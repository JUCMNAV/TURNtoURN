module Create;

create OUT : Urn from IN : Turn;

-- Contains all rules used to instantiate model items in other parts of the code.
-- This is a good place to look for potential improvements as it may be possible to
-- make some of these into matched rules (either now, if I have missed something),
-- or in future versions of ATL.

------------1/2 RULES WITH DO LOGIC--------------

rule connectNodeToNextNode(fromNode: Urn!PathNode, toNode: Urn!PathNode){
	to
		tNodeConnection: Urn!NodeConnection()
	do{
		fromNode.succ <- tNodeConnection;
		toNode.pred <- tNodeConnection;
	}
}

rule createEmptyPointConnect(currentNode: Turn!PathNode){
	to
		tConnect: Urn!Connect(
			id <- thisModule.nextId().toString(),
			name <- 'Connect' + tConnect.id,
			diagram <- thisModule.tUCMmap
		),
		tEmptyPt: Urn!EmptyPoint(
			id <- thisModule.nextId().toString(),
			name <- 'EmptyPoint' + tEmptyPt.id,
			diagram <- thisModule.tUCMmap
		)
	do{
		thisModule.connectNodeToNextNode(tConnect, thisModule.retrieveNode(currentNode.connectsTo));
		
		thisModule.connectNodeToNextNode(tEmptyPt, tConnect);
		
		thisModule.createNewConnection(tEmptyPt);

		thisModule.emptyPointMap <- thisModule.emptyPointMap.including(currentNode, tEmptyPt);
		thisModule.connectMap <- thisModule.connectMap.including(currentNode, tConnect);
	}
}

rule createAsynchEmptyPointConnect(currentNode: Turn!PathNode){
	using{
		tTimer : Urn!Timer = thisModule.resolveTemp(currentNode.connectsTo, 'tTimer');
		tNodeConnection2: Urn!NodeConnection = OclUndefined;
		tNodeConnection3: Urn!NodeConnection = OclUndefined;
	}
	to
		tConnect: Urn!Connect(
			id <- thisModule.nextId().toString(),
			name <- 'Connect' + tConnect.id,
			diagram <- thisModule.tUCMmap
		),
		tEmptyPt: Urn!EmptyPoint(
			id <- thisModule.nextId().toString(),
			name <- 'AsynchConnectTimer#' + currentNode.connectsTo.name,
			diagram <- thisModule.tUCMmap,
			succ <- tNodeConnection3
		)
	do{
		thisModule.connectNodeToNextNode(tConnect, tTimer);
		thisModule.connectNodeToNextNode(tEmptyPt, tConnect);
		
		tEmptyPt.succ <- thisModule.createNodeConnection();

		thisModule.emptyPointMap <- thisModule.emptyPointMap.including(tTimer, tEmptyPt);
		thisModule.emptyPointMap <- thisModule.emptyPointMap.including(tEmptyPt, tTimer);
		thisModule.connectMap <- thisModule.connectMap.including(currentNode, tConnect);
	}
}


------------2/2 RULES WITHOUT DO LOGIC-----------
-- or well, where the only "do" logic is a return

rule createAndJoin(){
	to
		tNodeConnection: Urn!NodeConnection(),
		tAndJoin: Urn!AndJoin(
			id <- thisModule.nextId().toString(),
			name <- 'AndJoin' + tAndJoin.id,
			succ <- tNodeConnection,
			diagram <- thisModule.tUCMmap
		)
	do{
		tAndJoin;
	}
}

rule createBeliefLink(s: Turn!ElementLink){
	to
		tBeliefLink: Urn!BeliefLink (
			target <- thisModule.resolveTemp(s.dest, 'tIntElemRef')
		)	
	do{
		tBeliefLink;
	}
}

rule createComponent(s: Turn!ComponentRef){
	to
		tComp: Urn!Component(
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName()
		)
	do{
		tComp;	
	}
}

rule createCondition(sourceExpression: String){
	to
	 	targetCondition: Urn!Condition(
	 		expression <- sourceExpression,
			label <- sourceExpression
	 	)
	do{
		targetCondition;
	}		
}

rule createConnect(){
	to
		tNodeConnection: Urn!NodeConnection(),
		tConnect: Urn!Connect(
			id <- thisModule.nextId().toString(),
			name <- 'Connect' + tConnect.id,
			succ <- tNodeConnection,
			diagram <- thisModule.tUCMmap
		)
	do{
		tConnect;	
	}
}

rule createEmptyPoint(){
	to
		tEmptyPt: Urn!EmptyPoint(
			id <- thisModule.nextId().toString(),
			name <- 'EmptyPoint' + tEmptyPt.id,
			diagram <- thisModule.tUCMmap
		)
	do{
		tEmptyPt;
	}
}

rule createEndPoint(node : Turn!PathNode){
	to
		tEndPoint: Urn!EndPoint (
			id <- thisModule.nextId().toString(),
			name <- 'EndPoint' + tEndPoint.id,
			label <- thisModule.createLabel(),
			diagram <- thisModule.tUCMmap,
			contRef <- thisModule.getUrnCompRef(node)
		)
	do{
		tEndPoint;
	}
}

rule createKpiEvalValueSet(s: Turn!LinearConversion){
	to
		tKpiEvalValueSet: Urn!KPIEvalValueSet(
			unit <- s.unit,
			targetValue <- s.targetValue,
			thresholdValue <- s.thresholdValue,
			worstValue <- s.worstValue
		)
	do{ 
		tKpiEvalValueSet;
	}
}

rule createLabel(){
	to
		tNodeLabel: Urn!NodeLabel()
	do{
		tNodeLabel;
	}
}

rule createMetadata(name: String, value: String){
	to 
 		targetMetadata: Urn!Metadata(
 			name <- name,
 			value <- value 
		)
	do{
	 	targetMetadata;
	}
}

rule createNodeConnection(){
	to
	 	tNodeConnection: Urn!NodeConnection()
	do{
		tNodeConnection;
	}		
}

rule createOrJoin(){
	to
		tNodeConnection: Urn!NodeConnection(),
		tOrJoin: Urn!OrJoin(
			id <- thisModule.nextId().toString(),
			name <- 'OrJoin' + tOrJoin.id,
			succ <- tNodeConnection,
			diagram <- thisModule.tUCMmap
		)
	do{
		tOrJoin;
	}
}

rule createQMapKpiEvalValueSet(conv: Turn!QualToQMappings){
	to		
		tQMapKpiEvalValueSet: Urn!KPIEvalValueSet(
			kpiConv <- conv	
		)
	do{
		tQMapKpiEvalValueSet;
	}
}

rule createResponsibility(s: Turn!RespRef){
	to
		tResp: Urn!Responsibility(
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName()
		)
	do{
		tResp;
	}
}