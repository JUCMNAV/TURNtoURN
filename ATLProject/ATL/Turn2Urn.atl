module Turn2Urn;
-- paths to metamodels for content assist
--@path Turn=/ATLProject/Metamodels/Turn.ecore
--@path Urn=/seg.jUCMNav/src/seg/jUCMNav/emf/urn.ecore

create OUT : Urn from IN : Turn;

--This helper initializes the value of id
-- CONTEXT: thisModule
-- RETURN: Integer

uses ConnectNodes;

----------------------helper variables-------------------------------

helper def: id : Integer = 0;

helper def: nodeCollector : Sequence(OclAny) = Sequence(OclAny) ;

helper def: counter : Integer = 1;

helper def: nodeCounter : Integer = 1;

helper def: orForkMap : Map(Turn!OrFork, Urn!OrFork) = Map{};

helper def: connectMap : Map(Turn!Connect, Urn!Connect) = Map{};

helper def: orForkNodeConnCounter : Integer = 1;

helper def: andForkMap : Map(Turn!OrFork, Urn!OrFork) = Map{};

helper def: endPointMap : Map(Turn!EndPoint, Urn!EndPoint) = Map{};

helper def: timerMap : Map(Turn!Timer, Urn!Timer) = Map{};

helper def: stubMap : Map(Turn!Stub, Urn!Stub) = Map{};

helper def: andForkNodeConnCounter : Integer = 1;

---------------helper methods-------------------------------------------
-- This helper creates an increment method for id
-- CONTEXT: Integer
-- RETURN: Integer
helper context Integer def : inc() : Integer = self + 1;

-- This helper checks if metadata already exists for a given parameter
-- CONTEXT: Turn!URNmodelElement
-- RETURN: Boolean
helper context Turn!URNmodelElement def: metadataExists(param : String) : Boolean = 
	self.metadata -> select(m| m.name.indexOf(param) <> -1 ) -> notEmpty();

helper context Turn!URNspec def: getActorElements() : Sequence(OclAny) = 
	self.actors -> collect(actor|actor.elems) -> flatten();

helper context Turn!UCMmap def: getPathNodes() : Sequence(OclAny) =
	self.paths -> flatten() -> collect(p|p.pathBody) -> flatten() -> collect(pb|pb.pathNodes) -> flatten();

helper context String def : capitalize(): String = self.substring(1,1).toUpper() + self.substring(2, self.size());

--This should only be called on TURN objects; I haven't been able to determine how to express that restriction using OCL, though.
helper context OclAny def : turnGetLongestName() : String = 
	if(not self.longName.oclIsUndefined())
	then
		if(not self.longName.longname.oclIsUndefined())
		then
			self.longName.longname
		else
			self.name	
		endif
	else
			self.name	
	endif;

helper context Turn!OrFork def : getPathBodies() : Turn!PathBody =
	self.body.regularBody -> collect(pb|pb.pathBody) -> flatten();

---------------imperative-method-like rules (sorry, spirit of declarative programming)-------------

rule nextId(){
	do{
		thisModule.id <- thisModule.id.inc();
		thisModule.id;
	}
}

rule processPathNode(node: OclAny, tUCMmap: Urn!UCMmap){
	do{
		if(node.oclIsTypeOf(Turn!Timer)){
			thisModule.connectTimerNodes(node, tUCMmap);
		}
		else if(node.oclIsTypeOf(Turn!AndFork)){
			thisModule.connectAndForkNodes(node, tUCMmap);
		}
		else if(node.oclIsTypeOf(Turn!OrFork)){
			thisModule.connectOrForkNodes(node, tUCMmap);
		}
		else if(node.oclIsTypeOf(Turn!Stub)){
			thisModule.createStubOutNodes(node, tUCMmap);
		}
	}
}

--TODO: to remove the boolean, take the logic that makes it necessary 
--and withdraw it into places this method is called and the boolean is true
rule processPathEnd(tUCMmap: Urn!UCMmap, pathend: Turn!PathNode, includeBacklinks: Boolean){
	do {
		thisModule.newNode <- '';
		
		if(pathend.oclIsTypeOf(Turn!EndpointWithConnect)){
			if(tUCMmap.nodes -> any(n|n.name = pathend.regularEnd.name).oclIsUndefined()){
				thisModule.createEndPoint(pathend.regularEnd, tUCMmap);
			}
			else if (includeBacklinks){
				thisModule.tEndPoint <- tUCMmap.nodes -> any(n|n.name = pathend.regularEnd.name);
				thisModule.ep <- pathend.regularEnd;
				if(thisModule.ep.epConnect.pbody.pathNodes -> isEmpty()){
					if(not thisModule.ep.epConnect.pbody.orFork.oclIsUndefined()){
						thisModule.condExpression <- thisModule.joinStringSequence(thisModule.ep.epConnect.pbody.orFork.condition.expression, '');
						thisModule.tEndPoint.pred <- thisModule.orForkMap.get(thisModule.ep.epConnect.pbody.orFork.orBody.orFork).succ -> any(s|s.condition.expression = thisModule.condExpression);
					}
				}
			}
		}
		else {
			if(pathend.oclIsTypeOf(Turn!OrFork)){
				thisModule.newNode <- thisModule.createOrFork(pathend, tUCMmap);
			}
			else if(pathend.oclIsTypeOf(Turn!Stub)){
				thisModule.newNode <- thisModule.createStub(pathend, tUCMmap);
				thisModule.createStubOutNodes(pathend, tUCMmap);
			}
			else if(pathend.oclIsTypeOf(Turn!AndFork)){
				thisModule.newNode <- thisModule.createAndFork(pathend, tUCMmap);
			}
		}
		
		thisModule.newNode;
	}
}

rule getUrnCompRef(obj: OclAny){
	do{
		thisModule.compRef <- '';
		
		if (not obj.boundTo.comp.oclIsUndefined()){
			thisModule.compRef <- thisModule.resolveTemp(obj.boundTo.comp, 'tCompRef');
		}
		else{
			thisModule.compRef <- thisModule.resolveTemp(obj.boundTo.comp2, 'tCompRef');
		}
		
		thisModule.compRef;
	}
}

--Dictionary. Had to use called rule as elseif is not supported in helpers
rule contributionNumberToKeyword(quantitativeContribution: Integer){
	do{
		thisModule.contribution <- 'Invalid';
		
		if (quantitativeContribution <= -1)
			thisModule.contribution <- 'Hurt';
		else if (quantitativeContribution < -49)
			thisModule.contribution <- 'SomeNegative';
		else if (quantitativeContribution = -100)
			thisModule.contribution <- 'Break';
		else if (quantitativeContribution = 0)
			thisModule.contribution <- 'Unknown';
		else if (quantitativeContribution < 50)
			thisModule.contribution <- 'Help';
		else if (quantitativeContribution <= 99)
			thisModule.contribution <- 'SomePositive';
		else if (quantitativeContribution = 100)
			thisModule.contribution <- 'Make';
		
		thisModule.contribution;
	}
}

rule joinStringSequence(s: String, delim: String){
	do{
		thisModule.joinedString <-'';
		for(currentString in s.content){
			thisModule.joinedString <- thisModule.joinedString + currentString + delim;	
		}
		thisModule.joinedString;
	}
}

rule collectReferencedEnds(pathNodes: Sequence(Turn!PathNode)){
	do{
		thisModule.nodes <- pathNodes -> flatten() -> collect(r|thisModule.resolveTemp(r,'tRespRef'));
		thisModule.nodes <- thisModule.nodes -> union(pathNodes -> flatten() -> collect(r|thisModule.resolveTemp(r,'tOrJoin')));
		thisModule.nodes <- thisModule.nodes -> union(pathNodes -> flatten() -> collect(r|thisModule.resolveTemp(r,'tAndJoin')));
		thisModule.nodes <- thisModule.nodes -> union(pathNodes -> flatten() -> collect(r|thisModule.resolveTemp(r,'tFailPt')));
		thisModule.nodes <- thisModule.nodes -> union(pathNodes -> flatten() -> collect(r|thisModule.resolveTemp(r,'tWtPlace')));
		thisModule.nodes;
	}
}

--TODO: It's worth considering use of the following method. However, I need to further consider
--how to design it best for usability before making use of it in the code.
rule getFromSequenceByName(seq: Sequence(OclAny), name: String){
	do{
		seq -> any(e|e.name=name);
	}
}

rule connectPathEndToNextNode(tUCMmap: Urn!UCMmap, pathend: Turn!Pathend, nextNode: Urn!PathNode){
	do {
		thisModule.finishedConnection <- true;
		
		if(pathend.oclIsTypeOf(Turn!EndpointWithConnect)){
			tUCMmap.nodes -> any(n|n.name = pathend.regularEnd.name).pred <- nextNode;
		}
		else if(pathend.oclIsTypeOf(Turn!OrFork)){
			thisModule.orForkMap.get(pathend).pred <- nextNode;
		}
		else if(pathend.oclIsTypeOf(Turn!AndFork)){
			thisModule.andForkMap.get(pathend).pred <- nextNode;
		}
		else if(pathend.oclIsTypeOf(Turn!Stub)){
			tUCMmap.nodes -> any(n|n.name = pathend.name).pred <- nextNode;
		}
		else{
			thisModule.finishedConnection <- false;
		}
		
		thisModule.finishedConnection;
	}
}

-- TODO: potential case of adding in union / including
rule processReferencedStub(tUCMmap: Turn!UCMmap, body: Turn!PathBody, nextNode: Turn!PathNode){
	do{
		thisModule.stub <- tUCMmap.nodes -> any(n|body.referencedStub.name=n.name);
		
		if(thisModule.stub.pred -> size() >= body.index){
			thisModule.orJoin <- thisModule.createOrJoin(tUCMmap, nextNode);
			thisModule.firstPred <- thisModule.stub.pred -> first();
			thisModule.orJoin.pred <- nextNode;
			--TODO: this overwrites the orJoin we just created; determine if this is intentional
			--TODO: verify if this does indeed match the original logic of the system
			thisModule.orJoin.pred <- thisModule.orJoin.pred -> union(thisModule.firstPred.source.succ);
			nextNode <- thisModule.orJoin.succ;
		}
		
		thisModule.stub.pred <- nextNode;
	}
}

--TODO: ensure all arguments in all calls of connectReferencedEnds are actually valid
--TODO: figure out if calling the argument "nextPred" in all calls is correct
rule connectReferencedEndToNextNode(tUCMmap: Turn!UCMmap, body: Turn!PathBody, position: Integer, nextNode: Turn!PathNode){
	do{
		if(not body.referencedEnd.oclIsUndefined()){
			thisModule.succeeded <- false;
			if(body.referencedEnd.oclIsTypeOf(Turn!EndpointWithConnect)){
				if(not body.pathNodes -> at(position).oclIsTypeOf(Turn!Connect)){
					if(tUCMmap.nodes -> any(n|body.referencedEnd.name=n.name).pred -> collect(s|s.source) -> isEmpty()){
						tUCMmap.nodes -> any(n|body.referencedEnd.name=n.name).pred <- nextNode;
						thisModule.succeeded <- true;
					}
				}
			}
			if(not thisModule.succeeded){
				if(body.referencedEnd.oclIsTypeOf(Turn!RespRef)){
					thisModule.resolveTemp(body.referencedEnd, 'tRespRef').pred <- nextNode;
				}
				else {
					tUCMmap.nodes -> any(n|body.referencedEnd.name=n.name).pred <- nextNode;
				}
			}
		}
		else if(not body.referencedStub.oclIsUndefined()){
			thisModule.processReferencedStub(tUCMmap, body, nextNode);
		}
	}
}

rule connectOrForkNodes(pathend : Turn!PathEnd, tUCMmap: Urn!UCMmap){
	do{
		thisModule.counter <- 1;
		if(not pathend.body.oclIsUndefined()){
			for(regularbody in pathend.body.regularBody -> flatten()){
				if(not regularbody.pathBody.referencedEnd.oclIsUndefined()){
					tUCMmap.nodes -> any(n|n.name=regularbody.pathBody.referencedEnd.name).pred <- tUCMmap.nodes -> any(n|n.name=pathend.name).succ -> at(thisModule.counter);
				}
				else{
					--if pathNodes exist
					if(regularbody.pathBody.oclIsTypeOf(Turn!PathBodyNodes)){
						if(not (regularbody.pathBody.pathNodes -> isEmpty())){
							thisModule.connectNodes(regularbody.pathBody, pathend, tUCMmap, thisModule.orForkMap.get(pathend));
						}	
					}

					if(regularbody.pathBody.pathEnd.oclIsTypeOf(Turn!Stub)){
						thisModule.createStubOutNodes(regularbody.pathBody.pathend, tUCMmap);
					}
					else{
						thisModule.processPathNode(regularbody.pathBody.pathEnd, tUCMmap);
					}
				}
			}		
			thisModule.counter <- thisModule.counter.inc();
		}
	}
}

rule connectOrForkEndPoints(obj: Turn!OrFork, nextNode: Urn!PathNode, tUCMmap: Urn!UCMmap, expression: String){
	do{
		thisModule.Pred <- '';
		
		if(nextNode.oclIsTypeOf(Urn!OrFork)){
			thisModule.nextPred <- nextNode.succ -> any(s|s.condition.expression = expression);
		}
		else{
			thisModule.nextPred <- nextNode.succ -> any(s|s.target.oclIsUndefined());
		}
		
		thisModule.body <- obj.connectingOrBody;
		
		thisModule.connectPathEndToNextNode(tUCMmap, thisModule.body.pathEnd, thisModule.nextPred);
		
		if(thisModule.body.referencedEnd.oclIsTypeOf(Turn!RespRef)){
			Urn!Responsibility.allInstances() -> any(n|thisModule.body.referencedEnd.name=n.name).pred <- thisModule.nextPred;
		}
		else{
			thisModule.connectReferencedEndToNextNode(tUCMmap, thisModule.body, thisModule.nodeCounter, thisModule.nextPred);
		}
	}
}

rule connectAndForkNodes(pathend : Turn!PathEnd, tUCMmap: Urn!UCMmap){
	do{
		thisModule.counter <- 1;
		for(pathbody in pathend.pathbody -> flatten()){
			thisModule.succeeded <- false;
			if(not pathbody.referencedEnd.oclIsUndefined()){
				if(not tUCMmap.nodes -> any(n|n.name=pathbody.referencedEnd.name).oclIsUndefined()){
					tUCMmap.nodes -> any(n|n.name=pathbody.referencedEnd.name).pred <- thisModule.andForkMap.get(pathend).succ -> at(thisModule.counter);
					thisModule.succeeded <- true;
				}
			}
			if(not thisModule.succeeded){
				--if pathNodes exist
				if(pathbody.oclIsTypeOf(Turn!PathBodyNodes)){
					if(pathbody.pathNodes -> size() > 0){
						-- The method below was missing its last argument, which led to a null pointer error.
						-- QUESTION: should thisModule.andForkMap.get(pathend) go here? I followed the example of connectOrForkNodes
						thisModule.connectNodes(pathbody, pathend, tUCMmap, thisModule.andForkMap.get(pathend));
					}	
					--if no pathNodes exist
					else if(pathbody.pathEnd.oclIsTypeOf(Turn!EndpointWithConnect)){
						tUCMmap.nodes -> any(n|pathbody.pathEnd.regularEnd.name=n.name).pred <- thisModule.andForkMap.get(pathend).succ -> at(thisModule.counter);
					}
				}
				thisModule.processPathNode(pathbody.pathEnd, tUCMmap);
			}
			thisModule.counter <- thisModule.counter.inc();
		}
	}
}

rule connectTimerNodes(timerObj:Turn!Timer, tUCMmap:Urn!UCMmap){
	do{
		--if pathNodes exist NOT SURE IF REQUIRED AS THERE IS NO PATHNODES TO TIMEOUT PATH
		if(not (timerObj.timeoutPath.pathNodes -> isEmpty())){
			--THIS WILL ALWAYS BE AT(2)
			tUCMmap.nodes -> any(n|n.name=timerObj.timeoutPath.pathNodes -> first().name).pred <- tUCMmap.nodes -> any(n|n.name=timerObj.name).succ -> at(2);
			thisModule.connectNodes(timerObj.timeoutPath, timerObj, tUCMmap, OclAny);
		}
		
		if(not timerObj.timeoutPath.referencedEnd.oclIsUndefined()){
			if(timerObj.timeoutPath.referencedEnd.oclIsTypeOf(Turn!RespRef)){
				--THIS WILL ALWAYS BE AT(2)
				Urn!Responsibility.allInstances() -> any(n|n.name=timerObj.timeoutPath.referencedEnd.name).respRefs -> first().pred <- tUCMmap.nodes -> any(n|n.name=timerObj.name).succ -> at(2);
			}
			--ADD MORE CODE
		}
		
		thisModule.processPathNode(timerObj.timeoutPath.pathEnd, tUCMmap);
	}
}

rule createMetadata(name: String, value: String) {
	to 
 		targetMetadata: Urn!Metadata(
 			name <- name,
 			value <- value 
		)
	do{
	 	targetMetadata;
	}
}

rule createNodeConns(){
	to
	 	targetNodeConn: Urn!NodeConnection
	do{
		targetNodeConn;	
	}		
}

rule createComponent(s: Turn!ComponentRef){
	to
		tComp: Urn!Component(
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName()
		)
	do{
		tComp;	
	}
}

rule createKpiEvalValueSet(s: Turn!LinearConversion) {
	to
		tkpiEvalValueSet: Urn! KPIEvalValueSet(
			unit <- s.unit,
			targetValue <- s.targetValue,
			thresholdValue <- s.thresholdValue,
			worstValue <- s.worstValue
		)
	do{ 
		tkpiEvalValueSet;
	}
}

rule createQMapKpiEvalValueSet(conv: Turn!QualToQMappings){
	to		
		tkpiEvalValueSet: Urn!KPIEvalValueSet(
			kpiConv <- conv	
		)
	do{ 
		tkpiEvalValueSet;
	}
}

rule createOrJoin(tUCMmap: Urn!UCMmap, predNode: Turn!PathNode){
	to
		tOrJoin: Urn!OrJoin(
			id <- thisModule.nextId().toString(),
			name <- 'OrJoin' + tOrJoin.id,
			succ <- tConn,
			diagram <- tUCMmap,
			pred <- predNode
		),
		tConn: Urn!NodeConnection()
	do{
		tOrJoin;
	}
}

rule createAndJoin(tUCMmap: Urn!UCMmap, predNode: Turn!PathNode){
	to
		tAndJoin: Urn!AndJoin(
			id <- thisModule.nextId().toString(),
			name <- 'AndJoin' + tAndJoin.id,
			succ <- tConn,
			diagram <- tUCMmap
		),
		tConn: Urn!NodeConnection()
	do{
		tAndJoin;
	}
}

rule createConnect(tUCMmap: Urn!UCMmap){
	to
		tConnect: Urn!Connect(
			id <- thisModule.nextId().toString(),
			name <- 'Connect' + tConnect.id,
			succ <- tConn,
			diagram <- tUCMmap
		),
		tConn: Urn!NodeConnection()
	do{
		tConnect;	
	}
}

rule createCondition(sourceExpression: String){
	to
	 	targetCondition: Urn!Condition(
	 		expression <- sourceExpression,
			label <- sourceExpression
	 	)
	do{
		targetCondition;
	}		
}

rule createOrForkNodeConns(ref: Turn!RegularOrFork, tUCMmap: Urn!UCMmap){
	to
	 	targetNodeConn: Urn!NodeConnection
	do{
		if(ref <> OclAny){
			targetNodeConn.condition <- thisModule.resolveTemp(ref.condition,'tCondition');
		}
		targetNodeConn;	
	}	
}

--QUESTION: this is just an empty object; it doesn't use the arguments. Should it do something?
rule createAndForkNodeConns(ref: Turn!PathBodyNodes, tUCMmap: Urn!UCMmap){
	to
	 	targetNodeConn: Urn!NodeConnection
	do{
		targetNodeConn;	
	}	
}

rule createResponsibility(s: Turn!RespRef){
	to
		tResp: Urn!Responsibility(
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName()
		)
	do{
		tResp;
	}
}

--QUESTION: in createOrFork this took an object as an argument but not in createAndFork; should it have one? the label is empty when made otherwise
rule createLabel(){
	to
		tNodeLabel: Urn!NodeLabel()
	do{
		tNodeLabel;
	}
}

rule createOrFork(obj:Turn!OrFork, tUCMmap: Urn!UCMmap){
	to
		tOrFork: Urn!OrFork (
			id <- thisModule.nextId().toString(),
			name <- obj.turnGetLongestName(),
			diagram <- tUCMmap
		)
	do{
		if(tOrFork.name.oclIsUndefined()){
			tOrFork.name <- 'OrFork'+tOrFork.id;
		}
		else{
			tOrFork.label <- thisModule.createLabel();
		}
		
		if (not obj.boundTo.oclIsUndefined()){
			tOrFork.contRef <- thisModule.getUrnCompRef(obj);
		}
		
		if(not obj.body.oclIsUndefined()){
			for(elem in obj.body.regularBody){
				tOrFork.succ <- thisModule.createOrForkNodeConns(elem, tUCMmap);
			}
		}
		else if(not obj.connectingOrBody.oclIsUndefined()){
			tOrFork.succ <- thisModule.createOrForkNodeConns(obj, tUCMmap);
		}
			
		thisModule.orForkMap <- thisModule.orForkMap.including(obj,tOrFork);
		
		--Connecting nodes before OrFork in case it is a stub
		if(not obj.pbody.parentStub.oclIsUndefined()){
			tOrFork.pred <- tUCMmap.nodes -> any(n|n.name = obj.pbody.parentStub.parentStub.name).succ ;
		}
		
		thisModule.nodeCollector <- Sequence{};
		
		if(not obj.body.oclIsUndefined()) {
			tUCMmap.nodes <- thisModule.collectReferencedEnds(obj.getPathBodies() -> collect(pb|pb.pathNodes));
			
			for (pathend in obj.getPathBodies() -> collect(pb|pb.pathEnd) -> flatten()){
				thisModule.newNode <- thisModule.processPathEnd(tUCMmap, pathend, true);
				
				if(not (thisModule.newNode = '')){
					thisModule.nodeCollector <- thisModule.nodeCollector -> union(thisModule.newNode);	
				}
			}

			--Orforks with only one pathbody
			if(obj.getPathBodies() -> size() = 1){
				--creating a node connection with a negated condition
				thisModule.createdOrForkNodeConnection <- thisModule.createOrForkNodeConns(OclAny, tUCMmap);
				
				thisModule.condition <- '';
				if(obj.getPathBodies().first().pathEnd.oclIsUndefined()){
					thisModule.condition <- thisModule.joinStringSequence(obj.getPathBodies().first().orFork.condition.expression, '');
				}
				thisModule.createdOrForkNodeConnection.condition <- thisModule.createCondition('!'+thisModule.condition);
				
				--creating an empty end point
				if(obj.body.elseBody.oclIsUndefined()){
					thisModule.createdOrForkNodeConnection.target <- thisModule.createEndPoint(obj, tUCMmap) -> first();
				}
				
				tOrFork.succ <- thisModule.createdOrForkNodeConnection;
			}
		}
		else{
			thisModule.createdOrForkNodeConnection <- thisModule.createOrForkNodeConns(OclAny, tUCMmap);
			
			thisModule.condition <- thisModule.joinStringSequence(obj.condition.expression, '');
			thisModule.createdOrForkNodeConnection.condition <- thisModule.createCondition('!'+thisModule.condition);
			thisModule.createdOrForkNodeConnection.target <- thisModule.createEndPoint(obj, tUCMmap) -> first();
			
			tOrFork.succ <- thisModule.createdOrForkNodeConnection;
		}
		
		if(not obj.connectingOrBody.oclIsUndefined()){
			tUCMmap.nodes <- thisModule.collectReferencedEnds(obj.connectingOrBody.pathNodes);
			
			thisModule.numberOfTerminatingNodes <- 0;
			if(not obj.body.oclIsUndefined()){
				thisModule.numberOfTerminatingNodes <- obj.body.regularBody -> collect(pb|pb.pathBody) -> select(pb|pb.pathEnd.oclIsUndefined()) -> size();
			}
			
			thisModule.newNode <- thisModule.processPathEnd(tUCMmap, obj.connectingOrBody.pathEnd, true);
			
			if(not (thisModule.newNode = '')){
				thisModule.nodeCollector <- thisModule.nodeCollector -> union(thisModule.newNode);	
			}
			
			thisModule.nextNode <- tOrFork;
			
			if(thisModule.numberOfTerminatingNodes = 2){
				thisModule.nextNode <- thisModule.createOrJoin(tUCMmap, tOrFork.succ -> any(s|s.target.oclIsUndefined()));
			}
			else if(thisModule.numberOfTerminatingNodes = 1){
				thisModule.firstPathNode <- obj.connectingOrBody.pathNodes -> first();
				
				if(not thisModule.firstPathNode.oclIsTypeOf(Turn!Connect)){
					if(not thisModule.firstPathNode.name.oclIsUndefined()){
						thisModule.nextNode <- tUCMmap.nodes -> any(n|n.name = thisModule.firstPathNode.name);
					}
				}
			}
			
			if(obj.connectingOrBody.pathNodes -> isEmpty()){
				if(not obj.body.oclIsUndefined()){
					if(not obj.body.elseBody.oclIsUndefined()){
						thisModule.connectOrForkEndPoints(obj, thisModule.nextNode, tUCMmap, thisModule.createdCondition.expression);
					}
					
					for (pathbody in obj.getPathBodies()){
						thisModule.condition <- '';
						if(pathbody.pathEnd.oclIsUndefined()){
							thisModule.condition <- thisModule.joinStringSequence(pathbody.orFork.condition.expression, '');
						}
						thisModule.connectOrForkEndPoints(obj, thisModule.nextNode, tUCMmap, thisModule.condition);
					}
				}
				else{
					thisModule.condition <- thisModule.joinStringSequence(obj.condition.expression, '');
					thisModule.connectOrForkEndPoints(obj, thisModule.nextNode, tUCMmap, thisModule.condition);
				}
			}
				
			thisModule.counter <- 1;
			thisModule.connectNodes(obj.connectingOrBody, obj, tUCMmap, thisModule.nextNode);
		}
		
		--Connecting nodes after OrFork
		thisModule.count <- 1;
		if(not obj.body.oclIsUndefined()){
			for(pathbody in obj.getPathBodies()){
				if(pathbody.pathNodes -> first().oclIsTypeOf(Turn!RespRef)){
					thisModule.resolveTemp(pathbody.pathNodes -> first(),'tRespRef').pred <- tOrFork.succ -> at(thisModule.count);
				}
				thisModule.count <- thisModule.count.inc(); 			
			}
		}
		
		--Using backlinks to connect orForks
		if(not obj.pbody.orFork.oclIsUndefined()){
			if(obj.pbody.pathNodes -> isEmpty()){
				tOrFork.pred <- thisModule.orForkMap.get(obj.pbody.orFork.orBody.orFork).succ -> at(thisModule.orForkNodeConnCounter);
				if(thisModule.orForkMap.get(obj.pbody.orFork.orBody.orFork).succ -> size() = thisModule.orForkNodeConnCounter){
					thisModule.orForkNodeConnCounter <- 1;
				}
				else{
					thisModule.orForkNodeConnCounter <- thisModule.orForkNodeConnCounter.inc();
				}
			}
		}
		thisModule.nodeCollector;
	}
}

rule createStub(stubObj:Turn!Stub, tUCMmap: Urn!UCMmap){
	to
		tStub: Urn!Stub (
			id <- thisModule.nextId().toString(),
			name <- stubObj.turnGetLongestName(),
			diagram <- tUCMmap,
			label <- tNodeLabel
		),
		tNodeLabel: Urn!NodeLabel()
	do{
		if(not stubObj.params.oclIsUndefined()){
			tStub.dynamic <- stubObj.params.plugin -> flatten() -> size() >= 2;
			tStub.bindings <- stubObj.params.plugin -> collect(p|thisModule.resolveTemp(p, 'tPluginBinding'));
		}
		else{
			tStub.bindings <- Turn!UCMmap.allInstances() -> any(m|m.name = tUCMmap.name).stubs -> collect(s|s.params.plugin) -> flatten() -> collect(p|thisModule.resolveTemp(p, 'tPluginBinding'));
		}
			
		if(not stubObj.stubType.oclIsUndefined()){
			if(stubObj.stubType.toString() = ('blocking').toString()){
				tStub.blocking <- true;
				tStub.dynamic <- true;
				tStub.synchronization <- true;
			}
			--original of the line below had no braces around the body of this conditional; was this a bug? 
			else if(stubObj.stubType.toString() = ('synchronizing').toString()){
				tStub.synchronization <- true;
				tStub.dynamic <- true;
			}
		}
		
		if(not stubObj.boundTo.oclIsUndefined()){
			--QUESTION: the two paths here do the same thing; is this accidental?
			if(not stubObj.boundTo.comp.oclIsUndefined()){
				tStub.contRef <- thisModule.resolveTemp(stubObj.boundTo.comp, 'tCompRef');
			}
			else{
				tStub.contRef <- thisModule.resolveTemp(stubObj.boundTo.comp, 'tCompRef');
			}
		}
		
		for(outPath in stubObj.outPaths -> flatten()){
			thisModule.stubOutPathNC <- thisModule.createNodeConns();
			tStub.succ <- thisModule.stubOutPathNC;
			if(outPath.threshold <> 0){
				thisModule.stubOutPathNC.threshold <- outPath.threshold.toString();
			}
		}
		
		thisModule.nodeCollector <- Sequence{};
		--collect referenced ends
		thisModule.pathNodes <- stubObj.outPaths -> flatten() -> collect(op|op.path) -> flatten() -> collect(p|p.pathNodes);
		tUCMmap.nodes <- thisModule.collectReferencedEnds(thisModule.pathNodes);
		
		thisModule.stubMap <- thisModule.stubMap.including(stubObj, tStub);
		
		thisModule.numberOfterminatingStubNodes <- 0;
		thisModule.outPathNumber <- Sequence{};
		for (outpath in stubObj.outPaths){
				if(outpath.path.pathEnd.oclIsUndefined()){
					--connecting pathnodes after andfork
					thisModule.numberOfterminatingStubNodes <- thisModule.numberOfterminatingStubNodes.inc();
					thisModule.outPathNumber <- thisModule.outPathNumber -> union(Sequence{outpath.index});
				}
		}
		-- collect regular ends
		for(pathend in stubObj.outPaths -> flatten() -> collect(op|op.path) -> flatten() -> collect(p|p.pathEnd) ){
			thisModule.newNode <- '';
			
			if(pathend.oclIsTypeOf(Turn!Timer)){
				thisModule.newNode <- thisModule.createTimer(pathend, tUCMmap);
			}
			else{
				thisModule.newNode <- thisModule.processPathEnd(tUCMmap, pathend, false);	
			}
			
			if(not (thisModule.newNode = '')){
				thisModule.nodeCollector <- thisModule.nodeCollector -> union(thisModule.newNode);	
			}
			
		}
		if(stubObj.outPaths -> isEmpty() and not stubObj.connectingStubBody.oclIsUndefined()){
			tStub.succ <- thisModule.createNodeConns();
			thisModule.outPathNumber <- Sequence{1};
		}
		
		--collecting nodes from connecting stub body
		if(not stubObj.connectingStubBody.oclIsUndefined()){ 
			
			tUCMmap.nodes <- thisModule.collectReferencedEnds(stubObj.connectingStubBody.pathNodes);
			
			thisModule.newNode <- thisModule.processPathEnd(tUCMmap, stubObj.connectingStubBody.pathEnd, true);
			
			if(not (thisModule.newNode = '')){
				thisModule.nodeCollector <- thisModule.nodeCollector -> union(thisModule.newNode);	
			}
			
			thisModule.nextNode <- tStub;
			if(thisModule.numberOfterminatingStubNodes > 1){
				thisModule.orJoin <- thisModule.createOrJoin(tUCMmap, tStub.succ -> at(thisModule.outPathNumber -> at(1))); 
				--TODO: determine if adding the use of 'including' below was correct
				thisModule.orJoin.pred <- thisModule.orJoin.pred -> including(tStub.succ -> at(thisModule.outPathNumber -> at(2)));
				thisModule.nextNode <- thisModule.orJoin;
				thisModule.outPathNumber <- Sequence{1};
				--thisModule.index <- 1;
			}
			else if(thisModule.numberOfterminatingStubNodes = 1){
				if(not stubObj.connectingStubBody.pathNodes -> first().oclIsUndefined()){
					if(not stubObj.connectingStubBody.pathNodes -> first().oclIsTypeOf(Turn!Connect)){
						if(not stubObj.connectingStubBody.pathNodes -> first().name.oclIsUndefined())
							thisModule.nextNode <- tUCMmap.nodes -> any(n|n.name = stubObj.connectingStubBody.pathNodes -> first());
					}
				}
			}
			
			thisModule.nextPred <- thisModule.nextNode.succ -> at(thisModule.outPathNumber -> at(1));
			thisModule.body <- stubObj.connectingStubBody;
			
			if(stubObj.connectingStubBody.pathNodes -> isEmpty()){
				thisModule.connectPathEndToNextNode(tUCMmap, thisModule.body.pathEnd, thisModule.nextPred);
				thisModule.connectReferencedEndToNextNode(tUCMmap, thisModule.body, thisModule.nodeCounter, thisModule.nextPred);
			}
				
			thisModule.counter <- 1;
			thisModule.connectNodes(stubObj.connectingStubBody, stubObj, tUCMmap, thisModule.nextNode);
			
		}
		--WRITE CODE FOR OUTPATH NODES as outpath has nodes too.
	
		--Using backlinks to connect orForks
		if(not stubObj.pbody.orFork.oclIsUndefined()){
			tStub.pred <- thisModule.orForkMap.get(stubObj.pbody.orFork.orBody.orFork).succ -> at(thisModule.orForkNodeConnCounter);
			if(thisModule.orForkMap.get(stubObj.pbody.orFork.orBody.orFork).succ -> size() = thisModule.orForkNodeConnCounter){
				thisModule.orForkNodeConnCounter <- 1;
			}
			else{
				thisModule.orForkNodeConnCounter <- thisModule.orForkNodeConnCounter.inc();
			}
		}
		thisModule.nodeCollector;
	}
}

rule createStubOutNodes(stubObj:Turn!Stub, tUCMmap:Urn!UCMmap){
	do{
		--create stubInPathCounter also...
		thisModule.stubOutPathCounter <- 1;
		for(outPath in stubObj.outPaths -> flatten()){
			--if no pathNodes exist
			if(stubObj.outPaths -> flatten() -> at(thisModule.stubOutPathCounter).path.pathNodes -> isEmpty()){
				thisModule.pathEnd <- outPath.path.pathEnd;
				
				if(not thisModule.pathEnd.oclIsUndefined()){
					thisModule.nextNode <- thisModule.stubMap.get(stubObj).succ -> at(thisModule.stubOutPathCounter);
					thisModule.finishedConnection < thisModule.connectPathEndToNextNode(tUCMmap, thisModule.pathEnd, thisModule.nextNode);
					
					if(not thisModule.finishedConnection){
						-- ADD MORE CODE BELOW
						if (not outPath.path.referencedEnd.oclIsUndefined()){
							tUCMmap.nodes -> any(n|n.name = outPath.path.referencedEnd.name).pred <- thisModule.nextNode;
						}
						else { 
							tUCMmap.nodes -> any(n|n.name = thisModule.pathend.name).pred <- thisModule.nextNode;
						}
					}
					
					if(thisModule.pathend.oclIsTypeOf(Turn!OrFork)){
						thisModule.connectOrForkNodes(thisModule.pathend, tUCMmap);
					}
					else if(thisModule.pathend.oclIsTypeOf(Turn!AndFork)){
						thisModule.createAndForkNodes(thisModule.pathend, tUCMmap);
					}
				}
			}
			--if pathNodes exist
			else{
				thisModule.nodeCounter <- 1;
				for(node in outPath.path.pathNodes){
					if(thisModule.nodeCounter = 1){
						if(node.oclIsTypeOf(Turn!RespRef)){
							Urn!Responsibility.allInstances() -> any(n|n.name = node.name).respRefs -> select(r|r.diagram.name = tUCMmap.name) -> flatten() -> any(ref|ref.pred -> isEmpty()).pred <- thisModule.stubMap.get(stubObj).succ -> at(thisModule.stubOutPathCounter);
						}
						else{	
							tUCMmap.nodes -> any(n|n.name = outPath.path.pathNodes -> first().name).pred <- thisModule.stubMap.get(stubObj).succ -> at(thisModule.stubOutPathCounter);
						}
					}
					else if(node.oclIsTypeOf(Turn!RespRef)){
						if(outPath.path.pathNodes -> at(thisModule.nodeCounter-1).oclIsTypeOf(Turn!RespRef)){
							thisModule.resolveTemp(node, 'tRespRef').pred <- thisModule.resolveTemp(outPath.path.pathNodes -> at(thisModule.nodeCounter-1), 'tRespRef').succ -> any(s|s.target.oclIsUndefined());	
						}
						else{
							thisModule.resolveTemp(node, 'tRespRef').pred <- tUCMmap.nodes -> any(n|outPath.path.pathNodes -> at(thisModule.nodeCounter-1).name=n.name).succ;
						}
					}
					else if(outPath.path.pathNodes -> at(thisModule.nodeCounter-1).oclIsTypeOf(Turn!RespRef)){
						tUCMmap.nodes -> any(n|n.name=node.name).pred <- thisModule.resolveTemp(outPath.path.pathNodes -> at(thisModule.nodeCounter-1), 'tRespRef').succ -> any(s|s.target.oclIsUndefined());
					}
					else{
						tUCMmap.nodes -> any(n|n.name=node.name).pred <- tUCMmap.nodes -> any(n|outPath.path.pathNodes -> at(thisModule.nodeCounter-1).name=n.name).succ;
					}
					
					if(thisModule.nodeCounter = outPath.path.pathNodes -> size()){
						if(outPath.path.pathNodes -> at(thisModule.nodeCounter).oclIsTypeOf(Turn!RespRef)){
							tUCMmap.nodes -> any(n|outPath.path.pathEnd.regularEnd.name=n.name).pred <- thisModule.resolveTemp(outPath.path.pathNodes -> at(thisModule.nodeCounter), 'tRespRef').succ -> any(s|s.target.oclIsUndefined());
						}
						else{
							tUCMmap.nodes -> any(n|outPath.path.pathEnd.name=n.name).pred <- tUCMmap.nodes -> any(n|n.name=outPath.path.pathNodes -> at(thisModule.nodeCounter).name).succ;
						}
					}
					thisModule.nodeCounter <- thisModule.nodeCounter.inc();
				}
			}
		 thisModule.stubOutPathCounter <- thisModule.stubOutPathCounter.inc();
		}	
	}
}

rule createAndFork(obj:Turn!AndFork, tUCMmap: Urn!UCMmap){
	to
		tAndFork: Urn!AndFork (
			id <- thisModule.nextId().toString(),
			name <- obj.turnGetLongestName(),
			diagram <- tUCMmap
		)
	do{
		if(tAndFork.name.oclIsUndefined()){
			tAndFork.name <- 'AndFork' + tAndFork.id;
		}
		else{
			tAndFork.label <- thisModule.createLabel();
		}
		
		thisModule.andForkMap <- thisModule.andForkMap.including(obj, tAndFork);
		
		for(elem in obj.pathbody){
			tAndFork.succ <- thisModule.createAndForkNodeConns(elem, tUCMmap);
		}
		
		if(not obj.boundTo.oclIsUndefined()){
			tAndFork.contRef <- thisModule.getUrnCompRef(obj);
		}
		
		thisModule.nodeCollector <- Sequence{};
		
		tUCMmap.nodes <- thisModule.collectReferencedEnds(obj.pathbody -> collect(pb|pb.pathNodes));
		
		for(pathend in obj.pathbody -> flatten() -> collect(pb|pb.pathEnd) -> flatten()){
			thisModule.newNode <- '';
			
			if(pathend.oclIsTypeOf(Turn!Timer)){
				thisModule.newNode <- thisModule.createTimer(pathend, tUCMmap);
			}
			else{
				thisModule.newNode <- thisModule.processPathEnd(tUCMmap, pathend, false);
			}
			
			if(not (thisModule.newNode = '')){
				thisModule.nodeCollector <- thisModule.nodeCollector -> union(thisModule.newNode);	
			}
		}
		
		if(not obj.connectingAndBody.oclIsUndefined()){	
			tUCMmap.nodes <- thisModule.collectReferencedEnds(obj.connectingAndBody.pathNodes);
			
			thisModule.numberOfTerminatingNodes <- obj.pathbody -> select(pb|pb.pathEnd.oclIsUndefined()) -> size();
			thisModule.newNode <- thisModule.processPathEnd(tUCMmap, obj.connectingAndBody.pathEnd, true);
			
			if(not (thisModule.newNode = '')){
				thisModule.nodeCollector <- thisModule.nodeCollector -> union(thisModule.newNode);	
			}
			
			--previously this was assigned to '' but that led to a compiler error
			--QUESTION: is setting this to tAndFork instead of '' correct? I followed the logic in createOrFork to do so
			thisModule.nextNode <- tAndFork;
			
			if(thisModule.numberOfTerminatingNodes = 2){
				thisModule.nextNode <- thisModule.createAndJoin(tUCMmap, tAndFork.succ -> any(s|s.target.oclIsUndefined()));
			}
			else if(thisModule.numberOfTerminatingNodes = 1){
				thisModule.firstPathNode <- obj.connectingAndBody.pathNodes -> first();
				
				if(not thisModule.firstPathNode.oclIsUndefined() and not thisModule.firstPathNode.oclIsTypeOf(Turn!Connect)){
					if(not thisModule.firstPathNode.name.oclIsUndefined()){
						thisModule.nextNode <- tUCMmap.nodes -> any(n|n.name = thisModule.firstPathNode.name);
					}
				}
			}
			
			if(obj.connectingAndBody.pathNodes -> isEmpty()){
				thisModule.nextPred <- thisModule.nextNode.succ -> any(s|s.target.oclIsUndefined());
				thisModule.connectPathEndToNextNode(tUCMmap, obj.connectingAndBody.pathEnd, thisModule.nextPred);
				thisModule.connectReferencedEndToNextNode(tUCMmap, obj.connectingAndBody.body, thisModule.nodeCounter, thisModule.nextPred);
			}
			thisModule.counter <- 1;
			thisModule.connectNodes(obj.connectingAndBody, obj, tUCMmap, thisModule.nextNode);
					
		}
		
		--Connecting nodes after AndFork
		thisModule.andForkNodeConnCounter <- 1;
		for(pathbody in obj.pathbody){
			if(pathbody.pathNodes -> first().oclIsTypeOf(Turn!RespRef)){
				thisModule.resolveTemp(pathbody.pathNodes -> first(),'tRespRef').pred <- tAndFork.succ -> at(thisModule.andForkNodeConnCounter);
			}
			thisModule.andForkNodeConnCounter <- thisModule.andForkNodeConnCounter.inc(); 			
		}
		
		--Using backlinks to connect orForks
		if(not obj.pbody.orFork.oclIsUndefined()){
			tAndFork.pred <- thisModule.orForkMap.get(obj.pbody.orFork.orBody.orFork).succ -> at(thisModule.orForkNodeConnCounter);
			if(thisModule.orForkMap.get(obj.pbody.orFork.orBody.orFork).succ -> size() = thisModule.orForkNodeConnCounter){
				thisModule.orForkNodeConnCounter <- 1;
			}
			else{
				thisModule.orForkNodeConnCounter <- thisModule.orForkNodeConnCounter.inc();
			}
		}
		thisModule.nodeCollector;
	}
}

rule createEndPoint(ep: Turn!EndPoint, tUCMmap: Urn!UCMmap){
	to
		tEndPoint: Urn!EndPoint (
			id <- thisModule.nextId().toString(),
			name <- 'EndPoint' + tEndPoint.id,
			diagram <- tUCMmap
		)
	do{
		--TODO: determine why this needs to be created here (as in the original) to match the original output
		-- i.e., why can't it be moved to the "to" block?
		tEndPoint.label <- thisModule.createLabel();
		
		if(ep.oclIsTypeOf(Turn!EndPoint)){
			tEndPoint.postcondition <- ep.condition;
		}
		
		if(not(ep <> OclAny)){
			tEndPoint.succ <- thisModule.createNodeConns();
		}
		else if(not ep.oclIsTypeOf(Turn!OrFork)){
			-- personal TODO: determine why name here isn't EndPoint(some#) (i.e., why we change it)
			tEndPoint.name <- ep.turnGetLongestName();
			
			thisModule.endPointMap <- thisModule.endPointMap.including(ep,tEndPoint);
			
			--component bindings
			if(not ep.boundTo.oclIsUndefined()){
				tEndPoint.contRef <- thisModule.getUrnCompRef(ep);
			}
	
			if(ep.epConnect.pbody.pathNodes -> isEmpty()){
				--Using backlinks to connect orFork
				thisModule.currentOrFork <- ep.epConnect.pbody.orFork;
				if(not thisModule.currentOrFork.oclIsUndefined()){
					thisModule.condExpression <- thisModule.joinStringSequence(thisModule.currentOrFork.condition.expression, '');
					tEndPoint.pred <- thisModule.orForkMap.get(thisModule.currentOrFork.orBody.orFork).succ -> any(s|s.condition.expression = thisModule.condExpression);
				}
				
				--Using backlinks to connect andFork
				thisModule.currentAndFork <- ep.epConnect.pbody.andFork;
				if(not thisModule.currentAndFork.oclIsUndefined()){
					tEndPoint.pred <- thisModule.andForkMap.get(thisModule.currentAndFork).succ -> any(s|s.target = OclUndefined);
				}
			}
		}
		
		thisModule.epNodeCollector <- Sequence{tEndPoint};
		thisModule.epNodeCollector;
	}
}

rule createTimer(timerObj: Turn!Timer, tUCMmap: Urn!UCMmap){
	to
		tTimer: Urn!Timer (
			id <- thisModule.nextId().toString(),
			name <- timerObj.turnGetLongestName(),
			succ <- tNodeConn,
			diagram <- tUCMmap,
			label <- tNodeLabel,
			waitType <- timerObj.kind.toString().capitalize()
		),
		tNodeConn: Urn!NodeConnection(
			condition <- timerObj.condition
		),
		tNodeLabel: Urn!NodeLabel()
	do{
		if(not timerObj.boundTo.oclIsUndefined()){
			tTimer.contRef <- thisModule.getUrnCompRef(timerObj);
		}
		
		thisModule.nodeCollector <- Sequence{};
		
		--timer out path
		if(not timerObj.timeoutPath.oclIsUndefined()){
			tTimer.succ <- thisModule.createNodeConns();
			if(not timerObj.timeoutCondition.oclIsUndefined()){
				tTimer.succ -> at(2).condition <- thisModule.createCondition(timerObj.timeoutCondition.expression.content -> at(1));
			}
			
			tUCMmap.nodes <- thisModule.collectReferencedEnds(timerObj.timeoutPath.pathNodes);
			
			thisModule.newNode <- '';
		
			if(timerObj.timeoutPath.pathEnd.oclIsTypeOf(Turn!Timer)){
				thisModule.newNode <- thisModule.createTimer(timerObj.timeoutPath.pathEnd, tUCMmap);
			}
			else{
				thisModule.newNode <- thisModule.processPathEnd(tUCMmap, timerObj.timeoutPath.pathEnd, false);
			}
			
			if(not (thisModule.newNode = '')){
				thisModule.nodeCollector <- thisModule.nodeCollector -> union(thisModule.newNode);	
			}
			
			-- THIS IS 4
			thisModule.body <- timerObj.timeoutPath;
			
			if(not thisModule.body.referencedStub.oclIsUndefined()){
				thisModule.next <- thisModule.nextNode.succ -> any(s|s.target.oclIsUndefined());
			}
			else{
				thisModule.next <- tTimer.succ -> at(2);
			}
			
			thisModule.connectReferencedEndToNextNode(tUCMmap, thisModule.body, thisModule.nodeCounter, thisModule.next);
		}
		
		thisModule.timerMap <- thisModule.timerMap.including(timerObj, tTimer);
		
		thisModule.nodeCollector;
	}
}

 --TODO: potential case of adding in unions / includings
rule createEmptyPt(tUCMmap: Urn!UCMmap, node: Turn!PathBodyNode, asynch: Boolean){
	to
		tEmptyPt: Urn!EmptyPoint(
			id <- thisModule.nextId().toString(),
			diagram <- tUCMmap
		)
	do{
		if (node <> OclAny){
			if(asynch){
				tEmptyPt.name <- 'AsynchConnectTimer#'+node.name;
				tEmptyPt.succ <- thisModule.createNodeConns();
			}
			else{
				tEmptyPt.name <- 'SynchConnectTimer#'+node.name;
			}
		}
		else{
			tEmptyPt.name <- 'EmptyPoint'+tEmptyPt.id;
			tEmptyPt.succ <- thisModule.createNodeConns();
			-- QUESTION: this is called twice; is it intentional?
			-- tEmptyPt.succ <- thisModule.createNodeConns();
		}
		tEmptyPt;	
	}
}

-----------------------matched rules------------------------------------

rule IntElements2IntElements{
	from 
		s: Turn!IntentionalElement (s.type.toString() <> 'belief')
	to 
		tIntElemRef: Urn!IntentionalElementRef (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
			def <- tIntElem,
			succ <- s.linksSrc -> collect(c|thisModule.resolveTemp(c,'tLinkRef'))
		),
		tIntElem: Urn!IntentionalElement (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
			type <- s.type.toString().capitalize(),
			linksSrc <- s.linksSrc
		)
		
	 do{
	 	if(not (Turn!Decomposition -> allInstances() -> select(d|d.dest=s)) -> isEmpty()){
			tIntElem.decompositionType <-Turn!Decomposition -> allInstances() -> select(d|d.dest=s) -> collect(a|a.decompositionType.toString().capitalize())-> first();
		}
		
		 --Added because the default value in enum is considered always.
		if(s.importance.toString().toLower().equals('none')){
			tIntElem.importanceQuantitative <- s.importanceQuantitative;
		}
		else{
			tIntElem.importance <- s.importance.toString().capitalize();
		}
		
	 	tIntElem.metadata <- thisModule.createMetadata('TURN-URN-NAME', s.name);
	}
}

rule Actor2Actor{
	from 
		s: Turn!Actor
	to 
		tActor: Urn!Actor (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
			importanceQuantitative <- s.importanceQuantitative
		),
		tActorRef: Urn!ActorRef (
			id <- thisModule.nextId().toString(),
			name <- 'ActorRef' + tActorRef.id,
			contDef <- tActor,
			nodes <- s.elems -> collect(e|thisModule.resolveTemp(e,'tIntElemRef')),
			nodes <- s.elems -> collect(e|thisModule.resolveTemp(e,'tBelief')),
			label <- tCompLabel
		),
		tCompLabel: Urn!ComponentLabel()
}

rule Belief2Belief {
	from
		s: Turn!IntentionalElement(s.type.toString() = 'belief')
	to 
	 	tBelief: Urn!Belief(
	 		id <- thisModule.nextId().toString(),
	 		name <- s.name,
			description <- s.name,
			succ <- tBeliefLink
	 	),
		tBeliefLink: Urn!BeliefLink(
			target <- s.linksSrc -> collect(l|l.dest) -> flatten() -> collect(l|thisModule.resolveTemp(l,'tIntElemRef')) -> first()
		)
}

rule Contribution2Contribution{
	from 
		s: Turn!Contribution (not (s.link.type.toString() = 'belief'))
	to 
		tCont: Urn!Contribution (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
		 	dest <- thisModule.resolveTemp(s.dest, 'tIntElem'),
			refs <- tLinkRef,
	 		correlation <- s.correlation
		),
		tLinkRef: Urn!LinkRef (
			link <- tCont,
			target <- thisModule.resolveTemp(s.dest,'tIntElemRef')
		)
	do{
		--Added because the default value in enum is considered always.
		if(s.contribution.toString().equals('undefined')){
			tCont.quantitativeContribution <- s.quantitativeContribution;
			tCont.contribution <- thisModule.contributionNumberToKeyword(s.quantitativeContribution);
		}
		else{
			tCont.contribution <- s.contribution.toString().capitalize();
		}
	}
}
	
rule Decomposition2Decomposition{
	from 
		s: Turn!Decomposition (not (s.link.type.toString() = 'belief'))
	to 
		tDecomp: Urn!Decomposition (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
		 	dest <- thisModule.resolveTemp(s.dest, 'tIntElem'),
			refs <- tLinkRef
		),
		tLinkRef: Urn!LinkRef (
			link <- tDecomp,
			target <- thisModule.resolveTemp(s.dest,'tIntElemRef')
		)
}

rule Dependency2Dependency{
	from 
		s: Turn!Dependency (not (s.link.type.toString() = 'belief'))
	to 
		tDep: Urn!Dependency (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
		 	dest <- s.dest,
			refs <- tLinkRef
		),
		tLinkRef: Urn!LinkRef (
			link <- tDep,
			target <- thisModule.resolveTemp(s.dest,'tIntElemRef')
		)
}

rule Strategies2Strategies{
	from
		s: Turn!EvaluationStrategy
	to
		tStrategies: Urn!EvaluationStrategy (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
			author <- s.info.author,
			evaluations <- s.evaluations,
			includedStrategies <- s.includedStrategies
		)
}

--TODO: potential case of adding in unions / includings
rule StrategiesGroup2StrategiesGroup{
	from
		s: Turn!StrategiesGroup
	to
		tStrategiesGroup: Urn!StrategiesGroup (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
			strategies <- s.evalStrategies -> including(s.evalStrategy)
		)
	
}

rule Evaluation2Evaluation {
	from
		s: Turn!Evaluation
 	to 
	 tEval : Urn!Evaluation (
	 		exceeds <- s.exceeds,
	 		intElement <- s.intElement,
			evaluation <- s.evaluation,
			qualitativeEvaluation <- s.qualitativeEvaluation.toString().capitalize()
	 )
	do {
		if(not s.conversion.oclIsUndefined()){
			if(s.conversion.oclIsTypeOf(Turn!QualToQMappings))
				tEval.kpiEvalValueSet <- thisModule.createQMapKpiEvalValueSet(s.conversion);
			else{
				tEval.kpiEvalValueSet <- thisModule.createKpiEvalValueSet(s.conversion);
			}
		}
	}
}

rule ContributionContext2ContributionContext{
	from
		s: Turn!ContributionContext
	to
		tContContext: Urn!ContributionContext (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
			changes <- s.changes,
			includedContexts <- s.includedContexts
		)
}

rule ContributionChange2ContributionChange{
	from
		s: Turn!ContributionChange
	to
		t: Urn!ContributionChange (
			contribution <- s.contribution,
			newQuantitativeContribution <- s.newQuantitativeContribution
		)
	do{
		if(not s.newContribution.toString().equals('undefined')){
			t.newContribution <- s.newContribution.toString().capitalize();
		}
	}
}

-- TODO: potential case of adding in union / including
rule ContributionContextGroup2ContributionContextGroup{
	from
		s: Turn!ContributionContextGroup
	to
		tContribGroups: Urn!ContributionContextGroup (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
			contribs <- s.contribs -> including(s.contrib)
		)
}

rule QMapping2QMapping {
	from 
		s: Turn!QualToQMapping
	to
		tQMapping: Urn!QualitativeMapping (
			realWorldLabel <- s.realWorldLabel,
			exceeds	<- s.exceeds,
			evaluation <- s.evaluation,
			qualitativeEvaluation <- s.qualitativeEvaluation.toString().capitalize()
		)
}

rule QMappings2QMappings {
	from
		s: Turn!QualToQMappings
	to
		tQMappings: Urn!QualitativeMappings (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
			mapping <- s.mappings
		)
}

rule Concern2Concern{
	from 
		s: Turn!Concern
	to 
		tConcern: Urn!Concern (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
		 	elements <- s.elements,
			condition <- s.condition,
			metadata <- s.metadata
		)
}

rule URNlink2URNlink{
	from 
		s: Turn!URNlink
	to 
		tCon: Urn!URNlink (
			type <- s.type,
			fromElem <- s.fromElem,
			toElem <- s.toElem
		)
}

--TODO: many potential cases of adding in unions / includings
rule Component2Component{
	from 
		s:Turn!ComponentRef 
	to
		tCompRef: Urn!ComponentRef(
			id <- thisModule.nextId().toString(),
			name <- 'ComponentRef' + tCompRef.id,
			label <- tCompLabel,
			diagram <- s.map
		),
		tCompLabel: Urn!ComponentLabel
	do{
		if(Urn!Component.allInstances() -> any(c|c.name = s.name).oclIsUndefined()){
			tCompRef.contDef <- thisModule.createComponent(s);	
		}
		else {
			tCompRef.contDef <- Urn!Component.allInstances() -> any(c|c.name = s.name);	
		}

		if(s.kind.toString() <> 'parent'){
			if(not s.kind.oclIsUndefined()){
				tCompRef.contDef.kind <- s.kind.toString().capitalize();
			}
			else{
				tCompRef.contDef.kind <- 'Team';
			}
		}
		
		tCompRef.contDef.protected <- s.protected;
		
		if(not s.boundTo.oclIsUndefined()){
			tCompRef.parent <- thisModule.getUrnCompRef(s);
		}
		
		if(s.element.oclIsTypeOf(Turn!LongBoundElement)){
			tCompRef.metadata <- thisModule.createMetadata('from',s.element.from);
			tCompRef.metadata <- tCompRef.metadata -> including(thisModule.createMetadata('to',s.element.to));
		}
		
		if(not (s.elements -> isEmpty())){
			for(elem in s.elements){
				if(elem.oclIsTypeOf(Turn!LongBoundElement)){
					tCompRef.metadata <- thisModule.createMetadata('from',elem.from);
					tCompRef.metadata <- tCompRef.metadata -> including(thisModule.createMetadata('to',elem.to));
				}
			}
		}
	}
}

rule Responsibility2Responsibility{
	from
		s: Turn!RespRef 
	to
		tRespRef: Urn!RespRef (
			succ <- tNodeConn,
			label <- tNodeLabel
		),
		tNodeConn: Urn!NodeConnection,
		tNodeLabel: Urn!NodeLabel
	do{
		--QUESTION: Should this be called "existingResponsibilities" instead? i.e., can we have multiple retrieved by this in any case?
		thisModule.existingResponsibility <- Urn!Responsibility.allInstances() -> any(r|r.name = s.turnGetLongestName());
		
		if(thisModule.existingResponsibility.oclIsUndefined()){
			tRespRef.respDef <- thisModule.createResponsibility(s);
		}
		else{
			tRespRef.respDef <- thisModule.existingResponsibility;
		}
		
		--component bindings added
		if (not s.boundTo.oclIsUndefined()){
			tRespRef.contRef <- thisModule.getUrnCompRef(s);
		}
		
		tRespRef.id <- thisModule.nextId().toString();
		tRespRef.name <- 'RespRef' + tRespRef.id;
	}
}

rule WaitingPlace2WaitingPlace{
	from		
		s: Turn!WaitingPlace
	to
		tWtPlace: Urn!WaitingPlace (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
			succ <- tNodeConn,
			label <- tNodeLabel,
			waitType <- s.kind.toString().capitalize()
		),
		tNodeConn: Urn!NodeConnection (
			condition <- s.condition
		),
		tNodeLabel: Urn!NodeLabel()
}

rule FailurePoint2FailurePoint{
	from		
		s: Turn!FailurePoint
	to
		tFailPt: Urn!FailurePoint (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
			succ <- tNodeConn,
			label <- tNodeLabel
		),
		tNodeConn: Urn!NodeConnection (
			condition <- s.condition
		),
		tNodeLabel: Urn!NodeLabel()
	do{
		if(not s.failLabel.oclIsUndefined()){
			tFailPt.expression <- s.failLabel.failure;
		}
	}
}

--TODO: many potential cases of adding in unions / includings
rule URNspec2URNspec{
	from 
		sURNspec: Turn!URNspec
	to
		tURNspec: Urn!URNspec(
			name <- sURNspec.name,
			metadata <- sURNspec.metadata,
			grlspec <- tGRLSpec,
			ucmspec <- tUcmspec,
			urnLinks <- sURNspec.urnlinks,
			urndef <- tURNdef
		),
		tURNdef: Urn!URNdefinition(
			specDiagrams <- sURNspec.ucmMaps -> collect(m|thisModule.resolveTemp(m,'tUCMmap')) -> including(tGRLSpecDiag),
			concerns <- sURNspec.concerns,
			components <- Urn!Component.allInstances(),
			responsibilities <- Urn!Responsibility.allInstances()
		),
		tGRLSpec: Urn!GRLspec(
			actors <- sURNspec.actors,
			intElements <- sURNspec.getActorElements() -> select(e|e.type.toString() <> 'belief'),
			groups <- sURNspec.stratGroups,
			strategies <- sURNspec.strategies,
			contributionGroups <- sURNspec.contribContextGroups,
			contributionContexts <- sURNspec.contribContexts,
			KPIConversion <- sURNspec.indConversions -> select(c|c.oclIsTypeOf(Turn!QualToQMappings))
		),
		tGRLSpecDiag: Urn!GRLGraph(
		 	nodes <- sURNspec.getActorElements() -> collect(e| thisModule.resolveTemp(e, 'tIntElemRef')) -> union(Urn!Belief.allInstances()),
			contRefs <- sURNspec.actors -> collect(a| thisModule.resolveTemp(a, 'tActorRef'))
		),
		tUcmspec: Urn!UCMspec
	do{
		tGRLSpec.links <- sURNspec.getActorElements() -> collect(e|e.linksSrc) -> flatten() -> collect(l|thisModule.resolveTemp(l,'tCont'));
		tGRLSpec.links <- tGRLSpec.links -> union(sURNspec.getActorElements() -> collect(e|e.linksSrc) -> flatten() -> collect(l|thisModule.resolveTemp(l,'tDecomp')));
		tGRLSpec.links <- tGRLSpec.links -> union(sURNspec.getActorElements() -> collect(e|e.linksSrc) -> flatten() -> collect(l|thisModule.resolveTemp(l,'tDep')));
		
		tGRLSpecDiag.connections <- sURNspec.getActorElements() -> collect(l|l.linksSrc) -> flatten() -> collect(m| thisModule.resolveTemp(m,'tLinkRef')); 
	 	tGRLSpecDiag.connections <- tGRLSpecDiag.connections -> union(sURNspec.getActorElements() -> collect(m| thisModule.resolveTemp(m,'tBeliefLink')));
		
		if(sURNspec.metadata -> select(m| m.name.indexOf('TURN-URN') <> -1 ) -> notEmpty()){
	 		if (sURNspec.metadata -> select(m|m.name.indexOf('nextGlobalID') <> -1 ) -> notEmpty()){
	 			thisModule.id <- sURNspec.metadata -> any(m|m.name = 'TURN-URN-nextGlobalID').value.toInteger();
	 		}
		}
		
		if (not sURNspec.info.oclIsUndefined()){
			tURNspec.description <- sURNspec.info.description;
			tURNspec.author <- sURNspec.info.author;
			tURNspec.created <- sURNspec.info.created;
			tURNspec.modified <- sURNspec.info.modified;
			tURNspec.specVersion <- sURNspec.info.specVersion;
			tURNspec.urnVersion <- sURNspec.info.urnVersion; 
		}
		
		--TODO: try to understand what the line below is doing
		tURNspec.nextGlobalID <- thisModule.id.toString();
	}
}

rule Metadata2Metadata{
	from 
		sourceMetadata: Turn!Metadata
	to 
		targetMetadata: Urn!Metadata(
			name <- sourceMetadata.name
		)
}

rule StartPoint2StartPoint{
	from
		s: Turn!StartPoint
	to
		tStartPoint: Urn!StartPoint (
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
			succ <- tNodeConn,
			label <- tNodeLabel,
			diagram <- s.path.map,
			precondition <- s.precondition
		),
		tNodeConn: Urn!NodeConnection(),
		tNodeLabel: Urn!NodeLabel()
	do{
		if(not s.boundTo.oclIsUndefined()){
			tStartPoint.contRef <- thisModule.getUrnCompRef(s);
		}
		
		if(not s.failKind.oclIsUndefined()){
			tStartPoint.failureKind <- s.failKind.toString().capitalize();
		}
	}
}

rule Condition2Condition{
	from 
		s: Turn!Condition
	to 
		tCondition: Urn!Condition(
			expression <- thisModule.joinStringSequence(s.expression, '')
		)
}

rule OrJoin2OrJoin{
	from 	
		s: Turn!OrJoin
	to
		tOrJoin: Urn!OrJoin(
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
			succ <- tNodeConn,
			label <- tNodeLabel
		),
		tNodeConn: Urn!NodeConnection,
		tNodeLabel: Urn!NodeLabel
	do{
		if(not s.boundTo.oclIsUndefined()){
			tOrJoin.contRef <- thisModule.getUrnCompRef(s);
		}
	}
}

rule AndJoin2AndJoin{
	from 	
		s: Turn!AndJoin
	to
		tAndJoin: Urn!AndJoin(
			id <- thisModule.nextId().toString(),
			name <- s.turnGetLongestName(),
			succ <- tNodeConn,
			label <- tNodeLabel
		),
		tNodeConn: Urn!NodeConnection,
		tNodeLabel: Urn!NodeLabel
	do{
		if(not s.boundTo.oclIsUndefined()){
			tAndJoin.contRef <- thisModule.getUrnCompRef(s);
		}
	}
}

--TODO: potential cases of adding in unions / includings
rule PluginBinding2PluginBinding{
	from 
		s:Turn!PluginBinding	
	to
		tPluginBinding: Urn!PluginBinding (
			plugin <- thisModule.resolveTemp(s.map, 'tUCMmap'),
			--This could be other way round, binding could be inBinding or OutBinding and vice-versa.
			precondition <- thisModule.resolveTemp(s.condition, 'tCondition'),
			replicationFactor <- s.replication
		)
	do{
		tPluginBinding.in  <- s.bindings -> collect(b|thisModule.resolveTemp(b,'tInBinding')) -> including(thisModule.resolveTemp(s.binding,'tInBinding'));
		tPluginBinding.out <- s.bindings -> collect(b|thisModule.resolveTemp(b,'tOutBinding')) -> including(thisModule.resolveTemp(s.binding,'tOutBinding'));	
	}
}

--TODO: modularize after test cases are working
rule InBinding2InBinding{
	from
		s:Turn!InBinding
	to
		tInBinding: Urn!InBinding(
			startPoint <- thisModule.resolveTemp(s.mapStart,'tStartPoint')
		)
	do{
		--QUESTION: does the order in which the if checks are performed matter?
		if(not s.binding.oclIsUndefined()){
			if(not s.binding.stubParam.stubDec.oclIsUndefined()){
				tInBinding.stubEntry <- thisModule.stubMap.get(s.binding.stubParam.stubDec.stub).pred -> at(s.index);
			}
			else{
				tInBinding.stubEntry <- thisModule.stubMap.get(s.binding.stubParam.stub).pred -> at(s.index);
			}
		}
		else if(not s.bindings.oclIsUndefined()){
			if(not s.bindings.stubParam.stubDec.oclIsUndefined()){
				tInBinding.stubEntry <- thisModule.stubMap.get(s.bindings.stubParam.stubDec.stub).pred -> at(s.index);
			}
			else{
				tInBinding.stubEntry <- thisModule.stubMap.get(s.bindings.stubParam.stub).pred -> at(s.index);
			}
		}
	}
}

--TODO: modularize after test cases are working
rule OutBinding2OutBinding{
	from
		s:Turn!OutBinding
	to
		tOutBinding: Urn!OutBinding(
			endPoint <- thisModule.endPointMap.get(s.mapEnd)
		)
	do{ 
		if(not s.bindings.oclIsUndefined()){
			if(not s.bindings.stubParam.stubDec.oclIsUndefined()){
				tOutBinding.stubExit <- thisModule.stubMap.get(s.bindings.stubParam.stubDec.stub).pred -> at(s.index);
			}
			else{
				tOutBinding.stubExit <- thisModule.stubMap.get(s.bindings.stubParam.stub).succ -> at(s.index);
			}
		}
		else if(not s.binding.oclIsUndefined()){
			if(not s.binding.stubParam.stubDec.oclIsUndefined()){
				tOutBinding.stubExit <- thisModule.stubMap.get(s.binding.stubParam.stubDec.stub).pred -> at(s.index);
			}
			else {
				tOutBinding.stubExit <- thisModule.stubMap.get(s.binding.stubParam.stub).succ -> at(s.index);
			}
		}
	}
}

rule UCMmap2UCMmap{
	from
		sUCMmap: Turn!UCMmap
	to
		tUCMmap: Urn!UCMmap (
			contRefs <- sUCMmap.components -> collect(c|thisModule.resolveTemp(c,'tCompRef')),
			nodes <- sUCMmap.paths -> flatten() -> collect(p|p.startPoint) -> flatten() -> collect(p|thisModule.resolveTemp(p,'tStartPoint')),
			connections <- sUCMmap.paths -> flatten() -> collect(p|p.startPoint) -> flatten() -> collect(p|thisModule.resolveTemp(p,'tNodeConn'))
		)
	do{
		--Referenced ends
		tUCMmap.nodes <- thisModule.collectReferencedEnds(sUCMmap.getPathNodes());
		
		--Regular ends
		--ADD CODE HERE
		for(path in sUCMmap.paths){
			thisModule.pathEnd <- path.pathBody.pathEnd;
			thisModule.newNode <- thisModule.processPathEnd(tUCMmap, thisModule.pathEnd, false);

			if(not (thisModule.newNode = '')){
				thisModule.nodeCollector <- thisModule.nodeCollector -> union(thisModule.newNode);	
			}
			
			if(thisModule.pathEnd.oclIsTypeOf(Turn!OrFork)) {
				thisModule.connectOrForkNodes(thisModule.pathEnd, tUCMmap);
			}
			else if(thisModule.pathEnd.oclIsTypeOf(Turn!AndFork)) {
				thisModule.connectAndForkNodes(thisModule.pathEnd, tUCMmap);
			}
		
			--connect nodes in a map
			thisModule.startPointName <- path.startPoint.turnGetLongestName();
			
			if(not path.startPoint.connect.oclIsUndefined() and tUCMmap.nodes -> select(n|n.oclIsTypeOf(Urn!EmptyPoint)) -> size() <> 0){
				tUCMmap.nodes -> select(n|n.oclIsTypeOf(Urn!EmptyPoint)) -> any(ept|ept.succ -> includes(thisModule.connectMap.get(path.startPoint.connect).pred -> first())).pred <- tUCMmap.nodes -> any(n|thisModule.startPointName=n.name).succ -> any(s|s.target.oclIsUndefined());
			}--if no pathnodes exist
			
			if(not thisModule.pathEnd.oclIsUndefined() and thisModule.pathEnd.oclIsTypeOf(Turn!EndpointWithConnect) and path.pathBody.pathNodes -> isEmpty()){
				thisModule.endPointName <- thisModule.pathEnd.regularEnd.turnGetLongestName();
				
				if(not thisModule.pathEnd.connect.oclIsUndefined()){
					--create succ for the connecting endpoint
					tUCMmap.nodes -> any(n|thisModule.endPointName=n.name).succ <- thisModule.createNodeConns();
					thisModule.connect <- thisModule.createConnect(tUCMmap);
					thisModule.connect.pred <- tUCMmap.nodes -> any(n|thisModule.endPointName=n.name).succ;
					thisModule.connectMap <- thisModule.connectMap.including(thisModule.pathEnd.connect, thisModule.connect);
					if(not thisModule.pathEnd.connect.connectsTo.oclIsTypeOf(Turn!Timer)){
						tUCMmap.nodes -> any(n|n.name = thisModule.pathEnd.connect.connectsTo.name).pred <- thisModule.connect.succ;
					}
					else{
						thisModule.createdEmptyPt <- thisModule.createEmptyPt(tUCMmap, thisModule.pathEnd.connect.connectsTo, false);
						thisModule.createdEmptyPt.pred <- thisModule.connect.succ;
					}
				}
				else if(not path.startPoint.connect.oclIsUndefined() and tUCMmap.nodes -> select(n|n.oclIsTypeOf(Urn!EmptyPoint)) -> size() <> 0){
					tUCMmap.nodes -> any(n|thisModule.endPointName=n.name).pred <- tUCMmap.nodes -> select(n|n.oclIsTypeOf(Urn!EmptyPoint)) -> any(ept|ept.succ -> includes(thisModule.connectMap.get(path.startPoint.connect).pred -> first())).succ -> any(s|s.target.oclIsUndefined());
				} 
				tUCMmap.nodes -> any(n|thisModule.endPointName=n.name).pred <- tUCMmap.nodes -> any(n|thisModule.startPointName=n.name).succ -> any(s|s.target.oclIsUndefined());
			}--added code for stub....
			else if (not thisModule.pathEnd.oclIsUndefined() and path.pathBody.pathNodes -> isEmpty() ){
				if(thisModule.pathEnd.oclIsTypeOf(Turn!OrFork)){
					thisModule.orForkMap.get(thisModule.pathEnd).pred <- tUCMmap.nodes -> any(n|thisModule.startPointName=n.name).succ;
				}
				else if(thisModule.pathEnd.oclIsTypeOf(Turn!AndFork)){
					thisModule.andForkMap.get(thisModule.pathEnd).pred <- tUCMmap.nodes -> any(n|thisModule.startPointName=n.name).succ;
				}
				else if(thisModule.pathEnd.oclIsTypeOf(Turn!Stub)){
					thisModule.stubMap.get(thisModule.pathEnd).pred <- tUCMmap.nodes -> any(n|thisModule.startPointName=n.name).succ;
				}
				
			}
			--if pathnodes exist
			else{
				thisModule.counter <- 1;
				thisModule.connectNodes(path.pathBody, path, tUCMmap, OclAny);
			}
			
			thisModule.next <- tUCMmap.nodes -> any(n|path.startPoint.name=n.name).succ;
			thisModule.body <- path.pathBody;
			
			if(not thisModule.body.referencedEnd.oclIsUndefined()){
				if(thisModule.body.referencedEnd.oclIsTypeOf(Turn!RespRef)){
					thisModule.resolveTemp(thisModule.body.referencedEnd,'tRespRef').pred <- thisModule.next;
				}			
				else{
					tUCMmap.nodes -> any(n|n.name=thisModule.body.referencedEnd.turnGetLongestName()).pred <- thisModule.next; 
				}
			}
			else{
				thisModule.connectReferencedEndToNextNode(tUCMmap, thisModule.body, thisModule.nodeCounter, thisModule.next);
			}
		}
		--Node connections
		tUCMmap.connections <- tUCMmap.nodes -> flatten() -> collect(of|of.succ) -> flatten();
		tUCMmap.id <- thisModule.nextId().toString();
		tUCMmap.name <- sUCMmap.turnGetLongestName();
	}
}