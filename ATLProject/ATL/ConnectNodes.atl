module ConnectNodes;

create OUT : Urn from IN : Turn;

rule retrieveNode(node: Turn!PathNode){
	using{
		vRetrievedNode : Turn!PathNode = OclUndefined;	
	}
	do{
		if(node.oclIsTypeOf(Turn!WaitingPlace)){
			vRetrievedNode <- thisModule.resolveTemp(node, 'tWtPlace');
		}
		else if(node.oclIsTypeOf(Turn!FailurePoint)){
			vRetrievedNode <- thisModule.resolveTemp(node, 'tFailPt');
		}
		else if(node.oclIsTypeOf(Turn!RespRef)){
			vRetrievedNode <- thisModule.resolveTemp(node, 'tRespRef');
		}
		else if(node.oclIsTypeOf(Turn!Timer)){
			thisModule.processTimer(node);	
			vRetrievedNode <- thisModule.resolveTemp(node, 'tTimer');
		}
		else if(node.oclIsTypeOf(Turn!AndFork)){
			vRetrievedNode <- thisModule.resolveTemp(node, 'tAndFork');
		}
		else if(node.oclIsTypeOf(Turn!OrFork)){
			vRetrievedNode <- thisModule.resolveTemp(node, 'tOrFork');
		}
		else if(node.oclIsTypeOf(Turn!Connect)){
			vRetrievedNode <- thisModule.resolveTemp(node, 'tConnect');
		}
		else{
			vRetrievedNode <- thisModule.tUCMmap.getNode(node.turnGetLongestName());
			--thisModule.debug(vRetrievedNode);
		}
		vRetrievedNode;
	}
}

rule getNodeToConnect(currentNode: Turn!PathNode){
	using{
		vNodeToJoin : Urn!PathNode = OclUndefined;
		vConnectedToWaitingPlace : Boolean = 
			if(currentNode.oclIsTypeOf(Turn!Connect)) then
				currentNode.connectsTo.oclIsTypeOf(Turn!WaitingPlace)
			else
				false
			endif;
	}
	do{
		if(vConnectedToWaitingPlace){
			vNodeToJoin <- thisModule.tUCMmap.getNode(currentNode.connectsTo.name).firstUnconnectedSucc();	
		}
		else{
			vNodeToJoin <- thisModule.tUCMmap.getNode(currentNode.name).succ;
		}
				
		vNodeToJoin; -- RETURN Urn!PathNode
	}
}

-- QUESTION: determine why we look at the first node, i.e., pathbody.pathNodes -> first(), rather than the last
-- TODO: determine how the next connecting node is chosen
rule handleConnectNode(currentNode: Turn!PathNode, newPred: Urn!PathNode){
	using{
		vNewNode : Urn!PathNode = OclUndefined;
	}
	to
		tNodeConnection1: Urn!NodeConnection(),
		tConnect: Urn!Connect(
			id <- thisModule.nextId().toString(),
			name <- 'Connect' + tConnect.id,
			diagram <- thisModule.tUCMmap,
			succ <- tNodeConnection1,
			pred <- tNodeConnection2
		),
		tNodeConnection2: Urn!NodeConnection(),
		tEmptyPt: Urn!EmptyPoint(
			id <- thisModule.nextId().toString(),
			name <- 'EmptyPoint'+tEmptyPt.id,
			diagram <- thisModule.tUCMmap,
			succ <- tNodeConnection2
		)
	do{
		if(currentNode.connectsTo.oclIsTypeOf(Turn!Timer)){
			tEmptyPt.name <- 'AsynchConnectTimer#'+currentNode.connectsTo.name;
			vNewNode <- tEmptyPt;
		}
		else{
			vNewNode <- thisModule.tUCMmap.getNode(currentNode.connectsTo.name);
			thisModule.addPred(vNewNode, tNodeConnection1);
		}
		thisModule.addPred(vNewNode, newPred);
		thisModule.connectMap <- thisModule.connectMap.including(currentNode, tConnect);
		tConnect;
	}
}

rule handleForkLogic(path: Turn!Path, firstNode: Turn!PathNode, nextNode: Urn!PathNode, case: String){
	do{
		if(nextNode.oclIsUndefined()){
			nextNode <- thisModule.retrieveNode(path);
		}

		if(firstNode.oclIsTypeOf(Turn!RespRef)){
			thisModule.addPred(thisModule.resolveTemp(firstNode ,'tRespRef'), nextNode.firstUnconnectedSucc());
		}
		else if(firstNode.oclIsTypeOf(Turn!Connect)){
			if(path.oclIsTypeOf(Turn!AndFork)){
				thisModule.handleConnectNode(firstNode, nextNode.firstUnconnectedSucc());
				
			}
			else if(path.oclIsTypeOf(Turn!OrFork)){
				thisModule.handleConnectNode(firstNode, nextNode.succ -> first());
			}
		}
		else{
			if(firstNode.oclIsTypeOf(Turn!Timer)){
				thisModule.processTimer(firstNode);
			}
			thisModule.addPred(thisModule.tUCMmap.getNode(firstNode.name), nextNode.firstUnconnectedSucc());
		}
		
		--ADDED FOR TIMER AND ORFORK CONNECTION
		if(firstNode.oclIsTypeOf(Turn!Timer)){
			thisModule.addPred(thisModule.tUCMmap.getNode(firstNode.name), thisModule.retrieveNode(path).succ -> first());
		}
	}
}

--QUESTION: there doesn't appear to be a Turn!PathNode class. What should I use instead?
rule handleFirstNode(path: Turn!Path, firstNode: Turn!PathNode, nextNode: Urn!PathNode){
	using {
		startPointFirstSucc : Urn!PathNode = OclUndefined;	
	}
	do{
		if(path.oclIsTypeOf(Turn!Path)){
			startPointFirstSucc <- thisModule.tUCMmap.getNode(path.startPoint.name).firstUnconnectedSucc();
			
			if(firstNode.oclIsTypeOf(Turn!WaitingPlace)){
				if(not firstNode.connect.oclIsUndefined() and not thisModule.tUCMmap.nodes -> select(n|n.oclIsTypeOf(Urn!EmptyPoint)) -> isEmpty()){
					thisModule.addPred(thisModule.tUCMmap.nodes -> select(n|n.oclIsTypeOf(Urn!EmptyPoint)) -> any(ept|ept.succ -> includes(thisModule.connectMap.get(firstNode.connect).pred -> first())), startPointFirstSucc);
				}
				else{
					thisModule.addPred(thisModule.resolveTemp(firstNode, 'tWtPlace'), startPointFirstSucc);
				}
			}
			else if(firstNode.oclIsTypeOf(Turn!Connect)) {
				thisModule.handleConnectNode(firstNode, startPointFirstSucc);
			}
			else{
				thisModule.addPred(thisModule.retrieveNode(firstNode), startPointFirstSucc);
			}
		}
		else if(path.oclIsTypeOf(Turn!OrFork) or path.oclIsTypeOf(Turn!AndFork)){
			thisModule.handleForkLogic(path, firstNode, nextNode);
		}
		else if(path.oclIsTypeOf(Turn!OrJoin) and firstNode.oclIsTypeOf(Turn!RespRef)){
			if(thisModule.getResponsibility(firstNode.name).respRefs -> select(r|r.diagram.name=thisModule.tUCMmap.name) -> isEmpty()){
				thisModule.addPred((thisModule.getResponsibility(firstNode.name).respRefs -> any(r|r.diagram.name=thisModule.tUCMmap.name)), thisModule.tUCMmap.getNode(path.name).succ);
			}
			else{
				thisModule.addPred(thisModule.tUCMmap.getNode(firstNode.name), thisModule.tUCMmap.getNode(path.name).succ -> first());
			}
		}
		else if(firstNode.oclIsTypeOf(Turn!Connect)){
			thisModule.handleConnectNode(firstNode, thisModule.resolveTemp(path, 'tStub').firstUnconnectedSucc());
		}
		else{
			thisModule.addPred(thisModule.retrieveNode(firstNode), thisModule.resolveTemp(path, 'tStub').firstUnconnectedSucc());
		}
	}
}
	
rule handleNode(pathbody: Sequence(Turn!PathBody), currentNode: Turn!PathNode, previousNode: Turn!PathNode){
	using{
		vCurrentNode : Turn!PathNode = OclUndefined;
		vPred : Turn!PathNode = OclUndefined;
	}
	do{
		if(currentNode.oclIsTypeOf(Turn!Connect)){
			if(previousNode.oclIsTypeOf(Turn!RespRef)){
				vPred <- thisModule.tUCMmap.allRespDefsMatching(previousNode.name) -> any(s|not s.pred.oclIsUndefined()).succ;
			}
			else if(previousNode.oclIsTypeOf(Turn!Connect)){
				vPred <- thisModule.tUCMmap.getNode(previousNode.connectsTo.name).firstUnconnectedSucc();
			}
			else{
				vPred <- thisModule.tUCMmap.getNode(previousNode.name).succ;
			}
			
			thisModule.connect <- thisModule.handleConnectNode(currentNode, vPred);
			
			if(previousNode.oclIsTypeOf(Turn!Connect)){
				if(not thisModule.connectMap.get(previousNode.connectsTo).oclIsUndefined()){
					thisModule.addPred(thisModule.connectMap.get(previousNode.connectsTo), thisModule.connect.succ);
				}
			}
		}
		else if(currentNode.oclIsTypeOf(Turn!Timer) or currentNode.oclIsTypeOf(Turn!WaitingPlace) or currentNode.oclIsTypeOf(Turn!FailurePoint) or currentNode.oclIsTypeOf(Turn!RespRef)){
			if(previousNode.oclIsTypeOf(Turn!Connect)){
				if(previousNode.connectsTo.oclIsTypeOf(Turn!Timer)){
					thisModule.pred <- thisModule.connectMap.get(previousNode).firstUnconnectedSucc(); 
				}
				else{
					if(previousNode.connectsTo.oclIsTypeOf(Turn!StartPoint)){
						thisModule.addSucc(thisModule.tUCMmap.getNode(previousNode.connectsTo.name), thisModule.createNodeConnection());
					}
					thisModule.pred <- thisModule.retrieveNode(previousNode.connectsTo).firstUnconnectedSucc();
				}
			}
			else{
				thisModule.pred <- thisModule.retrieveNode(previousNode).firstUnconnectedSucc();
			}
			
			thisModule.addPred(thisModule.retrieveNode(currentNode), thisModule.pred);		
		}
		-- DOUBLE CHECK IF THIS IS REQUIRED
		else if(previousNode.oclIsTypeOf(Turn!RespRef)){
			thisModule.addPred(thisModule.tUCMmap.getNode(currentNode.name), thisModule.tUCMmap.allRespDefsMatching(previousNode.name) -> collect(s|s.succ) -> flatten() -> select(s|s.target.oclIsUndefined()));
		}
		else if (previousNode.oclIsTypeOf(Turn!Connect)){
			if(previousNode.connectsTo.oclIsTypeOf(Turn!Timer)){
				thisModule.addPred(thisModule.tUCMmap.getNode(currentNode.name), thisModule.connectMap.get(previousNode).firstUnconnectedSucc()); 
			}
			else{
				if(previousNode.connectsTo.oclIsTypeOf(Turn!StartPoint)){
					thisModule.addSucc(thisModule.tUCMmap.getNode(previousNode.connectsTo.name).succ, thisModule.createNodeConnection());
				}
				thisModule.addPred(thisModule.tUCMmap.getNode(currentNode.name), thisModule.tUCMmap.getNode(previousNode.connectsTo.name).firstUnconnectedSucc());
			}
		}
		else{
			thisModule.addPred(thisModule.tUCMmap.getNode(currentNode.name), thisModule.tUCMmap.getNode(previousNode.name).succ);
		}
	}
}

rule handleRegularEnds(pathend: Turn!PathNode, currentNode: Turn!PathNode){
	using{
		vNewPred : Turn!PathNode = OclUndefined;	
	}
	do{
		if(currentNode.oclIsTypeOf(Turn!RespRef)){
			--QUESTION: is it possible for something to be both Turn!RespRef and Turn!Connect
			if(pathend.oclIsTypeOf(Turn!EndpointWithConnect) and currentNode.oclIsTypeOf(Turn!Connect)){
				thisModule.addPred(thisModule.tUCMmap.getNode(pathend.regularEnd.name), thisModule.tUCMmap.getNode(currentNode.connectsTo.name).succ);
			}
			else if (pathend.oclIsTypeOf(Turn!Stub)){
				thisModule.addPred(thisModule.resolveTemp(pathend, 'tStub'), thisModule.resolveTemp(currentNode ,'tRespRef').firstUnconnectedSucc());
			}
			else{
				thisModule.connectPathEndToNextNode(pathend, thisModule.resolveTemp(currentNode ,'tRespRef').firstUnconnectedSucc());
			}
		}
		else if(pathend.oclIsTypeOf(Turn!EndpointWithConnect)){
			if(currentNode.oclIsTypeOf(Turn!Connect)){
				if(currentNode.connectsTo.oclIsTypeOf(Turn!Timer)){
					vNewPred <- thisModule.connectMap.get(currentNode).firstUnconnectedSucc(); 
				}
				else{
					if(currentNode.connectsTo.oclIsTypeOf(Turn!StartPoint)){
						thisModule.addSucc(thisModule.retrieveNode(currentNode.connectsTo), thisModule.createNodeConnection());
					}
					vNewPred <- thisModule.tUCMmap.getNode(currentNode.connectsTo.name).firstUnconnectedSucc();
				}
			}
			else if(currentNode.oclIsTypeOf(Turn!WaitingPlace)){
				if(not currentNode.connect.oclIsUndefined() and not thisModule.tUCMmap.nodes -> select(n|n.oclIsTypeOf(Urn!EmptyPoint)) -> isEmpty()){
					vNewPred <- thisModule.tUCMmap.nodes -> select(n|n.oclIsTypeOf(Urn!EmptyPoint)) -> any(ept|ept.succ -> includes(thisModule.connectMap.get(currentNode.connect).pred -> first())).firstUnconnectedSucc();
				}
				else{
					vNewPred <- thisModule.retrieveNode(currentNode).firstUnconnectedSucc();
				}
			}
			else if(currentNode.oclIsTypeOf(Turn!Timer)){
				vNewPred <- thisModule.resolveTemp(currentNode, 'tTimer').firstUnconnectedSucc();
			}
			else{
				vNewPred <- thisModule.retrieveNode(currentNode).firstUnconnectedSucc();
			}	
			
			thisModule.addPred(thisModule.tUCMmap.getNode(pathend.regularEnd.name), vNewPred);
		}
		else if(pathend.oclIsTypeOf(Turn!OrFork) or pathend.oclIsTypeOf(Turn!AndFork)){
			thisModule.addPred(thisModule.retrieveNode(pathend), thisModule.getNodeToConnect(currentNode));
		}
		else if(pathend.oclIsTypeOf(Turn!Stub)){
			if(currentNode.oclIsTypeOf(Turn!Timer) or currentNode.oclIsTypeOf(Turn!Stub)){
				thisModule.addPred(thisModule.resolveTemp(pathend, 'tStub'), thisModule.retrieveNode(currentNode).firstUnconnectedSucc());
			}
			else{
				thisModule.addPred(thisModule.resolveTemp(pathend, 'tStub'), thisModule.getNodeToConnect(currentNode));
			}
		}	
	}
}

rule handleReferencedEnds(referencedEnd: Turn!PathNode, currentNode: Turn!PathNode){
	do{
		if(currentNode.oclIsTypeOf(Turn!RespRef)){
			if (referencedEnd.oclIsTypeOf(Turn!OrFork) or referencedEnd.oclIsTypeOf(Turn!AndFork)){
				thisModule.addPred(thisModule.retrieveNode(referencedEnd), thisModule.getResponsibility(currentNode.name).respRefs -> any(r|r.diagram.name = thisModule.tUCMmap.name).succ);
			}
			else if (referencedEnd.oclIsTypeOf(Turn!RespRef)){
				thisModule.addPred(thisModule.tUCMmap.anyRespDefMatching(referencedEnd.name), thisModule.tUCMmap.allRespDefsMatching(currentNode.name) -> collect(s|s.succ) -> flatten() -> select(s|s.target.oclIsUndefined()));
			}
			else{
				thisModule.addPred(thisModule.retrieveNode(referencedEnd), thisModule.tUCMmap.allRespDefsMatching(currentNode.name) -> collect(s|s.succ) -> flatten() -> select(s|s.target.oclIsUndefined()));
			}
		}
		else if(referencedEnd.oclIsTypeOf(Turn!OrFork) or referencedEnd.oclIsTypeOf(Turn!AndFork)){
			thisModule.addPred(thisModule.retrieveNode(referencedEnd), thisModule.tUCMmap.getNode(currentNode.name).firstUnconnectedSucc());
		}
		else if(referencedEnd.oclIsTypeOf(Turn!RespRef)){
			thisModule.addPred(thisModule.resolveTemp(referencedEnd, 'tRespRef'), thisModule.getNodeToConnect(currentNode));
		}
		else if(referencedEnd.oclIsTypeOf(Turn!EndpointWithConnect)){
			if(not currentNode.oclIsTypeOf(Turn!Connect)){
				if(thisModule.tUCMmap.getNode(referencedEnd.name).pred -> collect(s|s.source) -> isEmpty()){
					thisModule.addPred(thisModule.tUCMmap.getNode(referencedEnd.name), thisModule.tUCMmap.getNode(currentNode.name).succ);
				}
			}
		}
		else{
			thisModule.addPred(thisModule.tUCMmap.getNode(referencedEnd.name), thisModule.getNodeToConnect(currentNode));	
		}	
	}
}

rule handleReferencedStubs(pathbody: Sequence(Turn!Pathbody), currentNode: Turn!PathNode){
	using{
		vNextNode : Turn!PathNode = OclUndefined;	
	}
	do{
		if(currentNode.oclIsTypeOf(Turn!RespRef)){
			if(thisModule.tUCMmap.getNode(pathbody.referencedStub.name).pred -> size() >= pathbody.index){
				vNextNode <- thisModule.tUCMmap.anyRespDefMatching(currentNode.name).succ;
			}
			else{
				vNextNode <- thisModule.tUCMmap.allRespDefsMatching(currentNode.name) -> collect(s|s.succ) -> flatten() -> select(s|s.target.oclIsUndefined());
			}
		}
		else{
			vNextNode <- thisModule.getNodeToConnect(currentNode);
		}
		
		thisModule.processReferencedStub(pathbody, vNextNode);
	}
}
 
rule connectNodes(pathbody: Sequence(Turn!PathBody), path: Turn!Path, nextNode: Urn!PathNode){
	using{
		vPreviousNode : Turn!PathNode = OclUndefined;
		vLastNode : Turn!PathNode = pathbody.pathNodes -> last();
	}
	do{
		if(not pathbody.pathNodes -> isEmpty()){
			for (currentNode in pathbody.pathNodes){
				if(vPreviousNode.oclIsUndefined()){
					thisModule.handleFirstNode(path, currentNode, nextNode);
				}
				else{
					thisModule.handleNode(pathbody, currentNode, vPreviousNode);
				}
				
				if(currentNode <> vLastNode){
					vPreviousNode <- currentNode;
				}
			}
	
			if(not pathbody.pathEnd.oclIsUndefined()){
				thisModule.handleRegularEnds(pathbody.pathEnd, vLastNode);
			}
			else if(not pathbody.referencedEnd.oclIsUndefined()){
				thisModule.handleReferencedEnds(pathbody.referencedEnd, vLastNode);
			}
			else if(not pathbody.referencedStub.oclIsUndefined()){
				thisModule.handleReferencedStubs(pathbody, vLastNode, vPreviousNode);
			}
		}
	}
}