module Connect;

create OUT : Urn from IN : Turn;

-- Contains all rules used to handle connections between nodes.
-- Handles exploration of nodes to find PathBodies and connect 
-- the appropriate parts.

rule connectAndFork(sAndFork: Turn!AndFork){
	using{
		tAndFork : Urn!AndFork = thisModule.retrieveNode(sAndFork);
		tAndJoin : Urn!AndJoin = OclUndefined;
		tPathEnd : Urn!PathNode = OclUndefined;
		tLastNode : Urn!PathNode = OclUndefined;
	}
	do{
		if(not sAndFork.connectingAndBody.oclIsUndefined()){
			tAndJoin <- thisModule.createAndJoin();
		}
		
		for(pathbody in sAndFork.pathbody){			
			thisModule.collectPathBodyNodes(pathbody);
			tPathEnd <- thisModule.connectPathEnd(pathbody.pathEnd);
			
			if(tPathEnd.oclIsTypeOf(Urn!EmptyPoint)){
				thisModule.connect(tPathEnd, tAndJoin);
			}
			
			tLastNode <- thisModule.connectBodyNodes(pathbody.pathNodes, tAndFork);
			tPathEnd <- thisModule.getPathEnd(pathbody);
			thisModule.connect(tLastNode, tPathEnd);

			if(tPathEnd.oclIsTypeOf(Urn!EmptyPoint)){
				thisModule.connect(tPathEnd, tAndJoin);
			}
		}
		
		if(not sAndFork.connectingAndBody.oclIsUndefined()){
			tPathEnd <- thisModule.connectPathBody(sAndFork.connectingAndBody, tAndJoin);
		}
		
		-- Return the final node in the AndFork path or the first node of the following path.
		tPathEnd;
	}
}

rule connectBodyNodes(sPathNodes: Sequence(Turn!PathBodyNode), tPathStart: Urn!PathNode){
	using{
		tNode : Urn!PathNode = OclUndefined;
		tPreviousNode : Urn!PathNode = tPathStart;
	}
	do{
		for(sCurrentNode in sPathNodes){
			if(sCurrentNode.oclIsTypeOf(Turn!Timer)){
				thisModule.initTimer(sCurrentNode);
			}
			
			tNode <- thisModule.retrieveNode(sCurrentNode);
			thisModule.directConnect(tPreviousNode, tNode);
			tPreviousNode <- tNode;
		}
		
		tPreviousNode;
	}
}

rule connectEndpointWithConnect(sEndpointWithConnect: Turn!EndpointWithConnect){
	using{
		tEndPoint : Urn!EndPoint = OclUndefined;
	}
	do{
		if(sEndpointWithConnect.regularEnd.oclIsUndefined()){
			tEndPoint <- thisModule.createEndPoint();
		}
		else if(thisModule.retrieveNode(sEndpointWithConnect.regularEnd).oclIsUndefined()){
			tEndPoint <- thisModule.createEndPoint();
			thisModule.initEndPointUsingEndpointWithConnect(tEndPoint, sEndpointWithConnect);
		}
		else{
			-- EndPoint already exists
		}
		
		if(not tEndPoint.oclIsUndefined()){
			thisModule.endPointMap <- thisModule.endPointMap.including(sEndpointWithConnect.regularEnd, tEndPoint);
		}
		
		-- Return the Urn!EndPoint created using the Turn!EndpointWithConnect
		tEndPoint;
	}
}

rule connectOrFork(sOrFork: Turn!OrFork){
	using{
		tOrFork : Urn!OrFork = thisModule.retrieveNode(sOrFork);
		tOrJoin : Urn!OrJoin = OclUndefined;
		tPathEnd : Urn!PathNode = OclUndefined;
		tLastNode : Urn!PathNode = OclUndefined;
	}
	do{
		if(not sOrFork.connectingOrBody.oclIsUndefined()){
			tOrJoin <- thisModule.createOrJoin();
		}
		
		if(not sOrFork.body.oclIsUndefined()){
			for(regularOrFork in sOrFork.body.regularBody){
				thisModule.collectPathBodyNodes(regularOrFork.pathBody);
				tPathEnd <- thisModule.connectPathEnd(regularOrFork.pathBody.pathEnd);
				
				if(tPathEnd.oclIsTypeOf(Urn!EmptyPoint)){
					thisModule.connect(tPathEnd, tOrJoin);
				}
				
				tLastNode <- thisModule.connectBodyNodes(regularOrFork.pathBody.pathNodes, tOrFork);
				tPathEnd <- thisModule.getPathEnd(regularOrFork.pathBody);
				thisModule.connect(tLastNode, tPathEnd);
				tOrFork.succ.last().condition <- thisModule.resolveTemp(regularOrFork.condition, 'tCondition');
	
				if(tPathEnd.oclIsTypeOf(Urn!EmptyPoint)){
					thisModule.connect(tPathEnd, tOrJoin);
				}
			}
			
			if(not sOrFork.body.elseBody.oclIsUndefined()){
				tPathEnd <- thisModule.connectPathBody(sOrFork.body.elseBody, tOrFork);
				tOrFork.succ.last().condition <- thisModule.createCondition('else');
	
				if(tPathEnd.oclIsTypeOf(Urn!EmptyPoint)){
					thisModule.connect(tPathEnd, tOrJoin);
				}
			}
						
			--if(sOrFork.body.regularBody -> size() = 1 and sOrFork.connectingOrBody.oclIsUndefined()){
			--	tEmptyPoint <- thisModule.createEmptyPoint();
			--	thisModule.connect(tOrFork, tEmptyPoint);
			--	thisModule.connect(tEmptyPoint, thisModule.createEndPoint());
			--}
		}
		
		if(not sOrFork.connectingOrBody.oclIsUndefined()){
			tPathEnd <- thisModule.connectPathBody(sOrFork.connectingOrBody, tOrJoin);
		}
		
		-- Return the final node in the OrFork path or the first node of the following path.
		tPathEnd;	
	}
}

rule connectPathBody(sPathBody: Turn!PathBody, tPathStart: Urn!PathNode){
	using{
		tPathEnd : Urn!PathNode = OclUndefined;
		tLastNode : Urn!PathNode = OclUndefined;
	}
	do{
		thisModule.collectPathBodyNodes(sPathBody);
		thisModule.connectPathEnd(sPathBody.pathEnd);
		tLastNode <- thisModule.connectBodyNodes(sPathBody.pathNodes, tPathStart);
		tPathEnd <- thisModule.getPathEnd(sPathBody);
		thisModule.connect(tLastNode, tPathEnd);
		tPathEnd;
	}
}

rule connectPathEnd(sPathEnd: Turn!PathBodyNode){
	using{
		tPathEnd : Urn!PathNode = OclUndefined;
	}
	do{
		if(sPathEnd.oclIsTypeOf(Turn!AndFork)){
			tPathEnd <- thisModule.connectAndFork(sPathEnd);
		}
		else if(sPathEnd.oclIsTypeOf(Turn!EndpointWithConnect)){
			tPathEnd <- thisModule.connectEndpointWithConnect(sPathEnd);
		}
		else if(sPathEnd.oclIsTypeOf(Turn!OrFork)){
			tPathEnd <- thisModule.connectOrFork(sPathEnd);
		}
		else if(sPathEnd.oclIsTypeOf(Turn!Stub)){
			tPathEnd <- thisModule.connectStub(sPathEnd);
		}
		else{
			-- PathEnd is undefined
		}

		tPathEnd;
	}
}

rule connectStub(sStub: Turn!Stub){
	using{
		tStub : Urn!Stub = thisModule.retrieveNode(sStub);
		tOutPaths : Sequence(Urn!StubOutPath) = OclUndefined;
		tOrJoin : Urn!OrJoin = OclUndefined;
		tPathEnd : Urn!PathNode = OclUndefined;
		tLastNode : Urn!PathNode = OclUndefined;
	}
	do{
		thisModule.initStub(sStub);
		
		if(not sStub.connectingStubBody.oclIsUndefined()){
			tOrJoin <- thisModule.createOrJoin();
		}
		
		for(sOutPath in sStub.outPaths){
			thisModule.collectPathBodyNodes(sOutPath.path);
			tPathEnd <- thisModule.connectPathEnd(sOutPath.path.pathEnd);
			
			if(tPathEnd.oclIsTypeOf(Urn!EmptyPoint)){
				thisModule.connect(tPathEnd, tOrJoin);
			}
			
			tLastNode <- thisModule.connectBodyNodes(sOutPath.path.pathNodes, tStub);
			tPathEnd <- thisModule.getPathEnd(sOutPath.path);
			thisModule.connect(tLastNode, tPathEnd);
			
			if(sOutPath.threshold <> 0){
				tStub.succ.last().threshold <- sOutPath.threshold.toString();
			}
			
			if(not tPathEnd.oclIsTypeOf(Urn!EndPoint)){
				thisModule.connect(tPathEnd, tOrJoin);
			}
		}

		if(not sStub.connectingStubBody.oclIsUndefined()){
			if(sStub.outPaths -> isEmpty()){
				tLastNode <- thisModule.createEmptyPoint();
				thisModule.connect(tStub, tLastNode);
				thisModule.connect(tLastNode, tOrJoin);
			}
			
			tPathEnd <- thisModule.connectPathBody(sStub.connectingStubBody, tOrJoin);
		}
		
		-- Return the final node in the OrFork path or the first node of the following path.
		tPathEnd;	
	}
}

rule connectUCMmap(tUCMmap: Urn!UCMmap, paths: Sequence(Turn!Path)){
	using{
		tStartPoint : Urn!StartPoint = OclUndefined;	
	}
	do{
		thisModule.tUCMmap <- tUCMmap;

		for(sPath in paths){
			if(sPath.startPoint.oclIsUndefined()){
				tStartPoint <- thisModule.createStartPoint();
			}
			else{
				tStartPoint <- thisModule.retrieveNode(sPath.startPoint);
			}
			
			thisModule.connectPathBody(sPath.pathBody, tStartPoint);
		}
		
		tUCMmap.connections <- tUCMmap.nodes -> flatten() -> collect(of|of.succ) -> flatten();	
	}
}