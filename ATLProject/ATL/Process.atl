module Process;

create OUT : Urn from IN : Turn;

-- Contains all rules used to process a node in order to initialize it.
-- These rules are called in two cases. The first is in a matched rule,
-- to initialize the matched item. The second is in a called rule, to 
-- initialize some object created during a different object's matching.

-- TODO: implement
rule findPathBetweenNodes(tStartNode: Urn!PathNode, tEndNode: Urn!PathNode){
	using{
		vPath : Sequence(Urn!PathNode) = Sequence{tStartNode};	
	}
	do{
		vPath <- vPath.including(tEndNode);
		--thisModule.debug(vPath);
		
		--vPath;
		OclUndefined;
	}
}

rule connectAndFork(sAndFork: Turn!AndFork){
	using{
		tAndFork : Urn!AndFork = thisModule.retrieveNode(sAndFork);
		tAndJoin : Urn!AndJoin = OclUndefined;
		tPathEnd : Urn!PathBodyNode = OclUndefined;
	}
	do{
		if(not sAndFork.connectingAndBody.oclIsUndefined()){
			tAndJoin <- thisModule.createAndJoin();
		}
		
		for(pathbody in sAndFork.pathbody){
			thisModule.processPathBody(pathbody);
			tPathEnd <- thisModule.connectPathBody(pathbody, tAndFork);

			if(not sAndFork.connectingAndBody.oclIsUndefined() and not tPathEnd.oclIsTypeOf(Urn!EndPoint)){
				thisModule.connect(tPathEnd, tAndJoin);
			}
		}
		
		if(not sAndFork.connectingAndBody.oclIsUndefined()){
			thisModule.processPathBody(sAndFork.connectingAndBody);
			tPathEnd <- thisModule.connectPathBody(sAndFork.connectingAndBody, tAndJoin);
		}
		
		-- Return the final node in the AndFork path or the first node of the following path.
		tPathEnd;
	}
}

rule processContRef(tContRef: Urn!ComponentRef, nodes: Sequence(Urn!StartPoint)){
	using{
		tMetadata : Sequence(Urn!Metadata) = tContRef.metadata -> flatten();
		tFrom : String = 
			if tMetadata -> select(md|md.name='from') -> size() = 1 then
				tMetadata -> select(md|md.name='from').first().value
			else
				''
			endif;
		tTo : String = 
			if tMetadata -> select(md|md.name='to') -> size() = 1 then
				tMetadata -> select(md|md.name='to').first().value
			else
				''
			endif;
		tFromExists : Boolean = 
			(nodes -> select(n|n.name = tFrom) -> size() = 1)
			or (not nodes -> select(n|n.oclIsTypeOf(Urn!RespRef)) -> select(rr|rr.getRespName()=tFrom) -> isEmpty())
			and not (tFrom = '');
		tToExists : Boolean = 
			(not nodes -> select(n|n.name = tTo) -> isEmpty())
			or (not nodes -> select(n|n.oclIsTypeOf(Urn!RespRef)) -> select(rr|rr.getRespName()=tTo) -> isEmpty())
			and not (tTo = '');
		vStartNode : Urn!PathNode = OclUndefined;
		vEndNode : Urn!PathNode = OclUndefined;
	}
	do{
		if(tFromExists and tToExists){ --unidirectional, tFrom must be before tTo
			vStartNode <- nodes -> any(n|n.name = tFrom);

			if(vStartNode.oclIsUndefined()){
				vStartNode <- nodes -> select(n|n.oclIsTypeOf(Urn!RespRef)) -> any(rr|rr.getRespName()=tFrom);
			}
			
			vEndNode <- nodes -> any(n|n.name=tTo);
			
			if(vEndNode.oclIsUndefined()){
				vEndNode <- nodes -> select(n|n.oclIsTypeOf(Urn!RespRef)) -> any(rr|rr.getRespName()=tTo);
			}
			
			tContRef.nodes <- thisModule.findPathBetweenNodes(vStartNode, vEndNode);	
		}
	}
}

rule connectEndpointWithConnect(sEndpointWithConnect: Turn!EndpointWithConnect){
	using{
		tConnect : Urn!Connect = OclUndefined;
		tEndPoint : Urn!EndPoint = OclUndefined;
	}
	do{
		if(sEndpointWithConnect.regularEnd.oclIsUndefined()){
			tEndPoint <- thisModule.createEndPoint();
		}
		else if(thisModule.retrieveNode(sEndpointWithConnect.regularEnd).oclIsUndefined()){
			tEndPoint <- thisModule.createEndPoint();
			thisModule.initEndPointUsingEndpointWithConnect(tEndPoint, sEndpointWithConnect);
		}
		
		if(not tEndPoint.oclIsUndefined()){
			thisModule.endPointMap <- thisModule.endPointMap.including(sEndpointWithConnect.regularEnd, tEndPoint);
		}
		
		if(not sEndpointWithConnect.connect.oclIsUndefined()){
			tConnect <- thisModule.createConnect();
			thisModule.connect(thisModule.retrieveNode(sEndpointWithConnect.regularEnd), tConnect);
			thisModule.connect(tConnect, thisModule.retrieveNode(sEndpointWithConnect.connect));
		}
		
		-- Return the Urn!EndPoint created using the Turn!EndpointWithConnect
		tEndPoint;
	}
}

rule processOrFork(sOrFork: Turn!OrFork){
	using{
		tOrFork : Urn!OrFork = thisModule.retrieveNode(sOrFork);
		tOrJoin : Urn!OrJoin = OclUndefined;
		tPathEnd : Urn!PathNode = OclUndefined;

	}
	do{
		if(not sOrFork.connectingOrBody.oclIsUndefined()){
			tOrJoin <- thisModule.createOrJoin();
		}
		
		if(not sOrFork.body.oclIsUndefined()){
			for(regularOrFork in sOrFork.body.regularBody){
				thisModule.processPathBody(regularOrFork.pathBody);
				tPathEnd <- thisModule.connectPathBody(regularOrFork.pathBody, tOrFork);
				tOrFork.succ.last().condition <- thisModule.resolveTemp(regularOrFork.condition, 'tCondition');
	
				if(not sOrFork.connectingOrBody.oclIsUndefined() and not tPathEnd.oclIsTypeOf(Urn!EndPoint)){
					thisModule.connect(tPathEnd, tOrJoin);
				}
			}
			
			if(not sOrFork.body.elseBody.oclIsUndefined()){
				thisModule.processPathBody(sOrFork.body.elseBody);
				tPathEnd <- thisModule.connectPathBody(sOrFork.body.elseBody, tOrFork);
				tOrFork.succ.last().condition <- thisModule.createCondition('else');
	
				if(not sOrFork.connectingOrBody.oclIsUndefined() and not tPathEnd.oclIsTypeOf(Urn!EndPoint)){
					thisModule.connect(tPathEnd, tOrJoin);
				}
			}
						
			--if(sOrFork.body.regularBody -> size() = 1 and sOrFork.connectingOrBody.oclIsUndefined()){
			--	tEmptyPoint <- thisModule.createEmptyPoint();
			--	thisModule.connect(tOrFork, tEmptyPoint);
			--	thisModule.connect(tEmptyPoint, thisModule.createEndPoint());
			--}
		}
		
		if(not sOrFork.connectingOrBody.oclIsUndefined()){
			thisModule.processPathBody(sOrFork.connectingOrBody);
			thisModule.connectPathBody(sOrFork.connectingOrBody, tOrJoin);
		}
		
		tOrFork;	
	}
}


rule processPathBody(sPathBody: Turn!PathNode){
	using{
		tPathEnd : Urn!PathEnd = OclUndefined;	
	}
	do{
		thisModule.collectReferencedEnds(sPathBody.pathNodes);
		
		if(not sPathBody.pathEnd.oclIsUndefined()){
			if(sPathBody.pathEnd.oclIsTypeOf(Turn!EndpointWithConnect)){
				if(not sPathBody.pathEnd.regularEnd.oclIsUndefined()){
					thisModule.tUCMmap.nodes <- thisModule.tUCMmap.nodes.including(thisModule.retrieveNode(sPathBody.pathEnd));
				}
			}
			else{
				thisModule.tUCMmap.nodes <- thisModule.tUCMmap.nodes.including(thisModule.retrieveNode(sPathBody.pathEnd));
			}
			
			tPathEnd <- thisModule.processPathEnd(sPathBody.pathEnd);	
		}
		
		tPathEnd;
	}
}


rule processPathEnd(sPathEnd: Turn!PathBodyNode){
	using{
		tPathEnd : Urn!PathNode = OclUndefined;
	}
	do{
		if(sPathEnd.oclIsTypeOf(Turn!AndFork)){
			tPathEnd <- thisModule.connectAndFork(sPathEnd);
		}
		else if(sPathEnd.oclIsTypeOf(Turn!EndpointWithConnect)){
			tPathEnd <- thisModule.connectEndpointWithConnect(sPathEnd);
		}
		else if(sPathEnd.oclIsTypeOf(Turn!OrFork)){
			tPathEnd <- thisModule.processOrFork(sPathEnd);
		}
		else if(sPathEnd.oclIsTypeOf(Turn!Stub)){
			tPathEnd <- thisModule.processStub(sPathEnd);
		}

		tPathEnd;
	}
}

rule processStub(sStub: Turn!Stub){
	using{
		vPlugins : Sequence(Turn!Stub) = OclUndefined;
		tStub : Urn!Stub = thisModule.retrieveNode(sStub);
		tNextNode : Urn!PathNode = OclUndefined;
		tNextPred :  Urn!NodeConnection = OclUndefined;
		tOutPaths : Sequence(Urn!StubOutPath) = OclUndefined;
		tPathEnd : Urn!PathNode = OclUndefined;
		tOrJoin : Urn!OrJoin = OclUndefined;
	}
	do{
		if(not sStub.params.oclIsUndefined()){
			vPlugins <- sStub.params.plugin;
		}
		else{
			vPlugins <- Turn!UCMmap.allInstances() -> any(m|m.name=thisModule.tUCMmap.name).stubs -> collect(s|s.params.plugin) -> flatten();
		}
		
		tStub.bindings <- vPlugins -> collect(p|thisModule.resolveTemp(p, 'tPluginBinding'));
		
		for(outPath in sStub.outPaths){
			thisModule.createNewConnection(tStub);
			
			if(outPath.threshold <> 0){
				tStub.nextFreeConnection().threshold <- outPath.threshold.toString();
			}
			
			if(outPath.path.pathEnd.oclIsTypeOf(Turn!EndpointWithConnect)){
				thisModule.processPathBody(outPath.path);
				thisModule.connect(tStub, thisModule.getPathEnd(outPath.path));
			}
		}
		
		--collecting nodes from connecting stub body
		if(not sStub.connectingStubBody.oclIsUndefined()){
			thisModule.processPathBody(sStub.connectingStubBody);
			
			tOutPaths <- sStub.outPaths -> select(op|op.path.pathEnd.oclIsUndefined());
			tNextNode <- tStub;
			
			if(sStub.outPaths -> isEmpty()){
			--	thisModule.createNewConnection(tStub);
			}
			else if(tOutPaths -> size() = 1){
				if(not sStub.connectingStubBody.pathNodes.first().oclIsUndefined()){
					tNextNode <- thisModule.retrieveNode(sStub.connectingStubBody.pathNodes.first());
				}
			}
			else if(tOutPaths -> size() >= 2){ -- handle connecting OutPaths
				tOrJoin <- thisModule.createOrJoin();
				thisModule.createNewConnection(tOrJoin);
				thisModule.useConnection(tOrJoin, tStub.succ -> at(tOutPaths -> collect(op|op.index) -> at(1)));
				thisModule.useConnection(tOrJoin, tStub.succ -> at(tOutPaths -> collect(op|op.index) -> at(2)));
				tNextNode <- tOrJoin;
			}
			
			if(sStub.connectingStubBody.pathNodes -> isEmpty()){
				tPathEnd <- thisModule.getPathEnd(sStub.connectingStubBody);
				
				if(tOutPaths -> size() = 1){
					tNextPred <- tNextNode.succ -> at(tOutPaths.first().index);
				}
				else{
					tNextPred <- tNextNode.nextFreeConnection();
				}
				
				if(sStub.connectingStubBody.meetsOrJoinConditions(tPathEnd)){
					tOrJoin <- thisModule.createOrJoin();
					thisModule.connect(tStub, tOrJoin);
					thisModule.useConnection(tOrJoin, tNextPred);
					thisModule.useConnection(tOrJoin, tPathEnd.pred.first().source.succ);
					tNextNode <- tOrJoin;
				}
			}
			
			thisModule.connectPathBody(sStub.connectingStubBody, tNextNode);
		}
		
		tStub;
	}
}

rule processUCMmap(tUCMmap: Urn!UCMmap, paths: Sequence(Turn!Path)){
	do{
		thisModule.tUCMmap <- tUCMmap;

		for(path in paths){
			thisModule.connectPath(path);
		}
		
		tUCMmap.connections <- tUCMmap.nodes -> flatten() -> collect(of|of.succ) -> flatten();	
	}
}